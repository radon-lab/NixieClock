/*
  Arduino IDE 1.8.13 –≤–µ—Ä—Å–∏—è –ø—Ä–æ—à–∏–≤–∫–∏ 1.1.8 —Ä–µ–ª–∏–∑ –æ—Ç 30.01.26
  –°–ø–µ—Ü–∏–ª—å–Ω–æ –¥–ª—è –ø—Ä–æ–µ–∫—Ç–∞ "–ß–∞—Å—ã –Ω–∞ –ì–†–ò. –ê–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–∞—è –ø—Ä–æ—à–∏–≤–∫–∞"
  –°—Ç—Ä–∞–Ω–∏—Ü–∞ –ø—Ä–æ–µ–∫—Ç–∞ –Ω–∞ —Ñ–æ—Ä—É–º–µ - https://community.alexgyver.ru/threads/chasy-na-gri-alternativnaja-proshivka.5843/

  –ò—Å—Ö–æ–¥–Ω–∏–∫ - https://github.com/radon-lab/NixieClock
  –ê–≤—Ç–æ—Ä Radon-lab

  –ï—Å–ª–∏ –Ω–µ —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–æ —è–¥—Ä–æ ESP8266, "–§–∞–π–ª -> –ù–∞—Å—Ç—Ä–æ–π–∫–∏ -> –î–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã–µ —Å—Å—ã–ª–∫–∏ –¥–ª—è –ú–µ–Ω–µ–¥–∂–µ—Ä–∞ –ø–ª–∞—Ç", –≤ –æ–∫–Ω–æ –≤–≤–æ–¥–∞ –≤—Å—Ç–∞–≤–ª—è–µ—Ç–µ —Å—Å—ã–ª–∫—É - https://arduino.esp8266.com/stable/package_esp8266com_index.json
  –î–∞–ª–µ–µ "–ò–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã -> –ü–ª–∞—Ç–∞ -> –ú–µ–Ω–µ–¥–∂–µ—Ä –ø–ª–∞—Ç..." –Ω–∞—Ö–æ–¥–∏—Ç–µ –ø–ª–∞—Ç—É esp8266 –∏ —É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ—Ç–µ –≤–µ—Ä—Å–∏—é 2.7.4!

  –í "–ò–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã -> Flash Size" –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ –≤—ã–±—Ä–∞—Ç—å —Ä–∞—Å–ø—Ä–µ–¥–µ–ª–µ–Ω–∏–µ –ø–∞–º—è—Ç–∏ –≤ –∑–∞–≤–∏—Å–∏–º–æ—Å—Ç–∏ –æ—Ç —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–Ω–æ–≥–æ –æ–±—ä—ë–º–∞ FLASH:
  1–ú–ë - FS:64KB OTA:~470KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  2–ú–ë - FS:1MB OTA:~512KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  4–ú–ë - FS:2MB OTA:~1019KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  8–ú–ë - FS:6MB OTA:~1019KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
*/
#include "config.h"

#define GP_NO_DNS
#define GP_NO_MDNS

#include "web/src/GyverPortalMod.h"
GyverPortalMod ui;

#include "MEMORY.h"

#include <WiFiUdp.h>
WiFiUDP udp;

//–ø–µ—Ä–µ–º–µ–Ω–Ω—ã–µ
char buffSendIp[20]; //–±—É—Ñ–µ—Ä ip –∞–¥—Ä–µ—Å–∞
uint8_t buffSendAttempt; //–±—É—Ñ–µ—Ä –∫–æ–ª–∏—á–µ—Å—Ç–≤–∞ –ø–æ–ø—ã—Ç–æ–∫
uint8_t buffSendData[UDP_SEND_SIZE]; //–±—É—Ñ–µ—Ä –æ—Ç–ø—Ä–∞–≤–∫–∏

uint32_t rtcMemory[9]; //–ø–∞–º—è—Ç—å –Ω–∞–∂–∞—Ç–∏–π –∫–Ω–æ–ø–∫–∏ —Å–±—Ä–æ—Å–∞

boolean sendAnswerWait = false; //—Ñ–ª–∞–≥ –æ–∂–∏–¥–∞–Ω–∏—è –æ—Ç–≤–µ—Ç–∞ –æ—Ç —Ö–æ—Å—Ç–∞
uint8_t sendHostNum = 0; //—Ç–µ–∫—É—â–∏–π –Ω–æ–º–µ—Ä —Ö–æ—Å—Ç–∞
uint8_t sendHostAttempt = 0; //—Ç–µ–∫—É—â–∏–π –Ω–æ–º–µ—Ä –ø–æ–ø—ã—Ç–∫–∏ –æ—Ç–ø—Ä–∞–≤–∫–∏ –¥–∞–Ω–Ω—ã—Ö —Ö–æ—Å—Ç–∞
uint32_t sendAnswerTimer = 0; //—Ç–∞–π–º–µ—Ä –æ–∂–∏–¥–∞–Ω–∏—è –æ—Ç–≤–µ—Ç–∞ –æ—Ç —Ö–æ—Å—Ç–∞

boolean otaUpdate = true; //—Ñ–ª–∞–≥ –∑–∞–ø—Ä–µ—Ç–∞ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –µ—Å–ø
boolean settingsMode = false; //—Ñ–ª–∞–≥ —Ä–µ–∂–∏–º–∞ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ–Ω—Å–æ—Ä–∞

boolean sensorReady = false; //—Ñ–ª–∞–≥ –æ–∫–æ–Ω—á–∞–Ω–∏—è –∑–∞–º–µ—Ä–∞ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—ã
uint8_t sensorStartWait = 0; //–≤—Ä–µ–º—è –æ–∂–∏–¥–∞–Ω–∏—è –ø–æ—Å–ª–µ –ø–µ—Ä–≤–æ–≥–æ –∑–∞–ø—É—Å–∫–∞

uint16_t vccVoltage = 0; //–Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è

uint32_t sysCycleCount = 0; //—Å—á–µ—Ç—á–∏–∫ —Ü–∏–∫–ª–æ–≤ –ø—Ä–æ—Ü–µ—Å—Å–æ—Ä–∞

const uint8_t sleepTime[] = {1, 5, 10, 15, 30, 60};
const char sleepTimeList[] = "–ö–∞–∂–¥—É—é 1 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 5 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 10 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 15 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 30 –º–∏–Ω,–ö–∞–∂–¥—ã–π 1 —á–∞—Å";

//—Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞
struct sensorData {
  int16_t temp = 0x7FFF; //—Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞
  uint16_t press = 0; //–¥–∞–≤–ª–µ–Ω–∏–µ
  uint8_t hum = 0; //–≤–ª–∞–∂–Ω–æ—Å—Ç—å
} sens;

enum {
  SENS_DHT,
  SENS_DS,
  SENS_BME,
  SENS_SHT,
  SENS_AHT,
  SENS_MAX
};

const char *tempSensList[] = {"DHT", "DS18B20", "BMP/BME", "SHT", "AHT"};

#define REG_READ(reg) (*(volatile uint32*)(reg))

#define WIFI_SETTINGS ((uint8_t*)&rtcMemory[2])
#define WIFI_LOCAL_IP ((uint8_t*)&rtcMemory[4])
#define WIFI_GATEWAY_IP ((uint8_t*)&rtcMemory[5])
#define WIFI_SUBNET_MASK ((uint8_t*)&rtcMemory[6])
#define WIFI_DNS_1 ((uint8_t*)&rtcMemory[7])
#define WIFI_DNS_2 ((uint8_t*)&rtcMemory[8])

#if (LED_BUILTIN == TWI_SDA_PIN) || (LED_BUILTIN == TWI_SCL_PIN)
#undef STATUS_LED
#define STATUS_LED -1
#endif

#include "WIRE.h"
#include "AHT.h"
#include "SHT.h"
#include "BME.h"
#include "DHT.h"
#include "DS.h"

#include "WIFI.h"
#include "PING.h"

ADC_MODE(ADC_VCC); //—Ä–µ–∂–∏–º –∏–∑–º–µ—Ä–µ–Ω–∏—è –Ω–∞–ø—Ä—è–∂–µ–Ω–∏—è –ø–∏—Ç–∞–Ω–∏—è

void build(void) {
  GP.BUILD_BEGIN(GP_DEFAULT_THEME);

  GP.SELECT_LIST_STYLE(UI_BLOCK_COLOR, UI_BUTTON_COLOR);

  GP.PAGE_ZOOM(90, 390);
  GP.PAGE_BLOCK_BEGIN(700);

  //–æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –±–ª–æ–∫–æ–≤
  String updateList;
  updateList.reserve(500);
  updateList = F("barBat");

  //—Å—Å—ã–ª–∫–∏ –º–µ–Ω—é
  String menuLinks;
  menuLinks.reserve(100);
  menuLinks = F("/,/update,");

  //–Ω–∞—á–∞—Ç—å –º–µ–Ω—é
  if (ui.uri("/connection")) menuLinks += F("/connection");
  else if (ui.uri("/manual")) menuLinks += F("/manual");
  else menuLinks += F("/network");
  GP.NAV_BAR_LINKS(menuLinks, "–ò–Ω—Ñ–æ,–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ,–°–µ—Ç—å", 750, UI_MENU_COLOR);

  GP.BOX_BEGIN(GP_JUSTIFY, "100%;width:auto;padding-left:2%;padding-right:2%");
  GP.LABEL_BLOCK(getBatteryState(), "barBat", UI_BAR_BATTERY_COLOR, 18, 1);

  GP.BOX_BEGIN(GP_RIGHT, "100%");
  if (settings.sensor) {
    if (sens.temp != 0x7FFF) {
      updateList += F(",barTemp");
      GP.LABEL_BLOCK(String(sens.temp / 10.0, 1) + "¬∞–°", "barTemp", UI_BAR_TEMP_COLOR, 18, 1);
    }
    if (sens.hum) {
      updateList += F(",barHum");
      GP.LABEL_BLOCK(String(sens.hum) + "%", "barHum", UI_BAR_HUM_COLOR, 18, 1);
    }
    if (sens.press) {
      updateList += F(",barPress");
      GP.LABEL_BLOCK(String(sens.press) + "mm.Hg", "barPress", UI_BAR_PRESS_COLOR, 18, 1);
    }
  }
  else {
    GP.LABEL_BLOCK("-.-¬∞–°", "barTemp", UI_BAR_TEMP_COLOR, 18, 1);
  }
  GP.BOX_END();

  GP.BOX_END();
  GP.HR(UI_BAR_LINE_COLOR);

  if (ui.uri("/")) { //–∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è –æ —Å–∏—Å—Ç–µ–º–µ
    GP.PAGE_TITLE("–ò–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è");

    updateList += F(",infSignal,infPower,infUptime,infUsage");

    String sensorsList;
    sensorsList.reserve(150);
    sensorsList = "";

    GP.BLOCK_BEGIN(GP_THIN, "", "–î–∞—Ç—á–∏–∫ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—ã", UI_BLOCK_COLOR);
    for (uint8_t i = 0; i < SENS_MAX; i++) {
      if (settings.sensor & (0x01 << i)) {
        if (sensorsList[0] != '\0') sensorsList += '+';
        sensorsList += tempSensList[i];
      }
    }
    M_BOX(GP.LABEL("–î–∞—Ç—á–∏–∫", "", UI_LABEL_COLOR); GP.NUMBER("", (sensorsList[0] == '\0') ? "–û—Ç—Å—É—Ç—Å–≤—É–µ—Ç" : sensorsList, INT32_MAX, "", true););
    M_BOX(GP.LABEL("–ò–Ω—Ç–µ—Ä–≤–∞–ª", "", UI_LABEL_COLOR); GP.SELECT_LIST("extPeriod", sleepTimeList, settings.period););
    GP.BLOCK_END();

    GP.BLOCK_BEGIN(GP_THIN, "", "–°–∏—Å—Ç–µ–º–Ω–∞—è –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è", UI_BLOCK_COLOR);
    GP.BLOCK_HIDE_BEGIN();
    M_BOX(GP.LABEL("–£—Ä–æ–≤–µ–Ω—å —Å–∏–≥–Ω–∞–ª–∞", "", UI_LABEL_COLOR); GP.LABEL(stringGetPercent(wifiGetSignal()), "infSignal", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–†–µ–∂–∏–º –º–æ–¥–µ–º–∞", "", UI_LABEL_COLOR); GP.LABEL(wifiGetCurrentMode(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("MAC –∞–¥—Ä–µ—Å", "", UI_LABEL_COLOR); GP.LABEL(WiFi.macAddress(), "", UI_INFO_COLOR););

    if (wifiGetConnectStatus()) {
      GP.BREAK();
      M_BOX(GP.LABEL("SSID —Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(stringLengthConstrain(WiFi.SSID(), 12), "hint1", UI_INFO_COLOR););
      M_BOX(GP.LABEL("IP —Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(WiFi.localIP().toString(), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("–®–ª—é–∑ —Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(WiFi.gatewayIP().toString(), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("–ú–∞—Å–∫–∞ –ø–æ–¥—Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(WiFi.subnetMask().toString(), "", UI_INFO_COLOR););
      GP.HINT("hint1", WiFi.SSID()); //–≤—Å–ø–ª—ã–≤–∞—é—â–∞—è –ø–æ–¥—Å–∫–∞–∑–∫–∞
    }
    if (WiFi.getMode() != WIFI_STA) {
      GP.BREAK();
      M_BOX(GP.LABEL("SSID —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞", "", UI_LABEL_COLOR); GP.LABEL(stringLengthConstrain(AP_SSID, 12), "hint2", UI_INFO_COLOR););
      M_BOX(GP.LABEL("IP —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞", "", UI_LABEL_COLOR); GP.LABEL(WiFi.softAPIP().toString(), "", UI_INFO_COLOR););
      GP.HINT("hint2", AP_SSID); //–≤—Å–ø–ª—ã–≤–∞—é—â–∞—è –ø–æ–¥—Å–∫–∞–∑–∫–∞
    }

    GP.BREAK();
    GP.HR_TEXT("–ü–∞–º—è—Ç—å —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("–§—Ä–∞–≥–º–µ–Ω—Ç–∏—Ä–æ–≤–∞–Ω–æ(Heap)", "", UI_LABEL_COLOR); GP.LABEL(stringGetPercent(ESP.getHeapFragmentation()), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–°–≤–æ–±–æ–¥–Ω–æ(Heap)", "", UI_LABEL_COLOR); GP.LABEL(stringGetKilobyte(ESP.getFreeHeap(), 3), "", UI_INFO_COLOR););

    GP.BREAK();
    M_BOX(GP.LABEL("–í—Å–µ–≥–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(stringGetKilobyte(ESP.getFlashChipSize(), 1), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ó–∞–Ω—è—Ç–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(stringGetKilobyte(ESP.getSketchSize(), 1), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–°–≤–æ–±–æ–¥–Ω–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(stringGetKilobyte(ESP.getFreeSketchSpace(), 1), "", UI_INFO_COLOR););

    GP.BREAK();
    GP.HR_TEXT("–ü–∞—Ä–∞–º–µ—Ç—Ä—ã", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("ID —á–∏–ø–∞", "", UI_LABEL_COLOR); GP.LABEL(stringGetHex(ESP.getChipId()), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ß–∞—Å—Ç–æ—Ç–∞ –ø—Ä–æ—Ü–µ—Å—Å–æ—Ä–∞", "", UI_LABEL_COLOR); GP.LABEL(stringGetFreq(ESP.getCpuFreqMHz()), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ó–∞–≥—Ä—É–∑–∫–∞ –ø—Ä–æ—Ü–µ—Å—Å–æ—Ä–∞", "", UI_LABEL_COLOR); GP.LABEL(stringGetPercent(systemGetUsage()), "infUsage", UI_INFO_COLOR););

    GP.BREAK();
    M_BOX(GP.LABEL("–ù–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è", "", UI_LABEL_COLOR); GP.LABEL(stringGetVoltage(getBatteryVoltage()), "infPower", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–í—Ä–µ–º—è —Ä–∞–±–æ—Ç—ã", "", UI_LABEL_COLOR); GP.LABEL(stringGetTimeFromMs(millis()), "infUptime", UI_INFO_COLOR););

    GP.BREAK();
    GP.HR_TEXT("–í–µ—Ä—Å–∏—è –ü–û", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("SDK", "", UI_LABEL_COLOR); GP.LABEL(ESP.getSdkVersion(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("CORE", "", UI_LABEL_COLOR); GP.LABEL(ESP.getCoreVersion(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("GyverPortal", "", UI_LABEL_COLOR); GP.LABEL(GP_VERSION, "", UI_INFO_COLOR););

    GP.BREAK();
    M_BOX(GP.LABEL("UID", "", UI_LABEL_COLOR); GP.LABEL(getSensorId(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("Firmware", "", UI_LABEL_COLOR); GP.LABEL(ESP_FIRMWARE_VERSION, "", UI_INFO_COLOR););

    GP.BREAK();
    GP.BLOCK_END();
    GP.BLOCK_END();

    GP.FOOTER_BEGIN();
    GP.TEXT_LINK("https://github.com/radon-lab/", "@radon_lab", "user", "#bbb");
    GP.BREAK();
    GP.TEXT_LINK("https://community.alexgyver.ru/threads/chasy-na-gri-v2-alternativnaja-proshivka.5843/", "–û–±—Å—É–∂–¥–µ–Ω–∏–µ –Ω–∞ —Ñ–æ—Ä—É–º–µ", "forum", "#e67b09");
    GP.FOOTER_END();
  }
  else if (ui.uri("/update") && otaUpdate) { //–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ ESP
    GP.PAGE_TITLE("–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ");

    GP.BLOCK_BEGIN(GP_THIN, "", "–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏", UI_BLOCK_COLOR);
    GP.SPAN("–ü—Ä–æ—à–∏–≤–∫—É –º–æ–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å –≤ Arduino IDE: –°–∫–µ—Ç—á -> –≠–∫—Å–ø–æ—Ä—Ç –±–∏–Ω–∞—Ä–Ω–æ–≥–æ —Ñ–∞–π–ª–∞ (—Å–æ—Ö—Ä–∞–Ω—è–µ—Ç—Å—è –≤ –ø–∞–ø–∫—É —Å –ø—Ä–æ—à–∏–≤–∫–æ–π).", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP.SPAN("–ü–æ–¥–¥–µ—Ä–∂–∏–≤–∞–µ–º—ã–µ —Ñ–æ—Ä–º–∞—Ç—ã —Ñ–∞–π–ª–æ–≤: bin –∏ bin.gz.", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP.HR_TEXT("–ó–∞–≥—Ä—É–∑–∏—Ç—å —Ñ–∞–π–ª—ã", UI_LINE_COLOR, UI_HINT_COLOR);
    M_BOX(GP.LABEL("–ü—Ä–æ—à–∏–≤–∫–∞ ESP", "", UI_LABEL_COLOR); GP.OTA_FIRMWARE("üì•", UI_BUTTON_COLOR, true););
    GP.VOID_BOX("0;height:10px");
    GP.BLOCK_END();
  }
  else { //–ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ –∫ —Ä–æ—É—Ç–µ—Ä—É
    GP.PAGE_TITLE("–°–µ—Ç–µ–≤—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏");

    GP.BLOCK_BEGIN(GP_THIN, "", "–õ–æ–∫–∞–ª—å–Ω–∞—è —Å–µ—Ç—å WIFI", UI_BLOCK_COLOR);
    if (wifiGetConnectStatus() || wifiGetConnectWaitStatus()) { //–ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ –∫ —Å–µ—Ç–∏
      if (!wifiGetConnectStatus()) {
        updateList += F(",extConnect,extNetwork");
        GP.RELOAD("extConnect");
      }

      GP.FORM_BEGIN("/network");
      GP.TEXT("", "", wifiGetLocalSSID(), "", 0, "", true);
      GP.BREAK();
      GP.TEXT("", "", wifiGetLocalIP(), "", 0, "", true);
      GP.SPAN(wifiGetConnectState(), GP_CENTER, "extNetwork", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
      GP.HR(UI_LINE_COLOR);
      GP.SUBMIT((wifiGetConnectStatus()) ? "–û—Ç–∫–ª—é—á–∏—Ç—å—Å—è" : "–û—Ç–º–µ–Ω–∞", UI_BUTTON_COLOR);
      GP.FORM_END();
    }
    else { //–≤—ã–±–æ—Ä —Å–µ—Ç–∏
      if (wifiGetScanCompleteStatus()) wifiResetScanCompleteStatus();

      updateList += F(",extScan");
      GP.RELOAD("extScan");

      GP.FORM_BEGIN("/connection");
      if (ui.uri("/manual")) { //—Ä—É—á–Ω–æ–π —Ä–µ–∂–∏–º –≤–≤–æ–¥–∞ —Å–µ—Ç–∏
        GP.TEXT("wifiSsid", "SSID", settings.ssid, "", 64);
        GP.BREAK();
        GP.PASS_EYE("wifiPass", "–ü–∞—Ä–æ–ª—å", settings.pass, 64);
        GP.BREAK();
        GP.TEXT_LINK("/network", "–°–ø–∏—Å–æ–∫ —Å–µ—Ç–µ–π", "net", UI_LINK_COLOR);
        GP.HR(UI_LINE_COLOR);
        GP.BOX_BEGIN(GP_CENTER, "300px");
        GP.SUBMIT("–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", UI_BUTTON_COLOR);
        GP.BUTTON("extClear", "‚úï", "", (!settings.ssid[0] && !settings.pass[0]) ? GP_GRAY : UI_BUTTON_COLOR, "65px", (boolean)(!settings.ssid[0] && !settings.pass[0]), true);
        GP.BOX_END();
      }
      else { //–≤—ã–±–æ—Ä —Å–µ—Ç–∏ –∏–∑ —Å–ø–∏—Å–∫–∞
        GP.SELECT_LIST("wifiSsid", wifi_scan_list, 0, 0, wifiGetScanFoundStatus());
        GP.BREAK();
        GP.PASS_EYE("wifiPass", "–ü–∞—Ä–æ–ª—å", settings.pass, 64);
        GP.BREAK();
        GP.TEXT_LINK("/manual", "–†—É—á–Ω–æ–π —Ä–µ–∂–∏–º", "net", UI_LINK_COLOR);
        GP.HR(UI_LINE_COLOR);
        GP.BOX_BEGIN(GP_CENTER, "300px");
        if (wifiGetScanFoundStatus()) GP.BUTTON("", "–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", "", GP_GRAY, "90%", true);
        else GP.SUBMIT("–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", UI_BUTTON_COLOR);
        GP.BUTTON("extScan", "<big><big>‚Üª</big></big>", "", UI_BUTTON_COLOR, "65px", false, true);
        GP.BOX_END();
      }
      GP.FORM_END();
    }
    GP.BLOCK_END();

    if (wifiGetConnectStatus()) { //—Å–µ—Ç–µ–≤—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
      GP.BLOCK_BEGIN(GP_THIN, "", "–û—Ç–ø—Ä–∞–≤–∫–∞ –¥–∞–Ω–Ω—ã—Ö", UI_BLOCK_COLOR);

      IPAddress addrSend;
      for (uint8_t i = 0; i < MAX_CLOCK; i++) {
        if (i) {
          GP.HR(UI_LINE_COLOR);
        }
        addrSend = settings.send[i];
        if (addrSend) {
          M_BOX(
            GP.TEXT("", "IP –∞–¥—Ä–µ—Å", addrSend.toString() + " [x" + settings.attempt[i] + ']', "90%;max-width:408px", 20, "", true);
            GP.BUTTON_MINI(String("extSendDel/") + i, "–£–¥–∞–ª–∏—Ç—å", "", UI_BUTTON_COLOR, "auto;min-width:110px", false, true);
          );
        }
        else {
          M_BOX(
            M_BOX(GP_LEFT, GP.TEXT("extSendIp", "IP –∞–¥—Ä–µ—Å", "", "", 15); GP.NUMBER("extSendAttempt", "–ü–æ–ø—ã—Ç–æ–∫", INT32_MAX, "", false););
            GP.BUTTON_MINI("extSendAdd", "–î–æ–±–∞–≤–∏—Ç—å", "", UI_BUTTON_COLOR, "auto;min-width:110px", false, true);
          );
          buffSendIp[0] = '\0';
          buffSendAttempt = 1;
          break;
        }
      }
      GP.BLOCK_END();
    }
  }

  GP.UPDATE(updateList);

  GP.PAGE_BLOCK_END();
  GP.BUILD_END();
}
//--------------------------------------------------------------------
void buildUpdate(bool UpdateEnd, const String & UpdateError) {
  GP.BUILD_BEGIN(GP_DEFAULT_THEME);

  GP.PAGE_ZOOM(90, 390);
  GP.PAGE_MIDDLE_ALIGN();
  GP.PAGE_BLOCK_BEGIN(500);

  GP.PAGE_TITLE("–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ");

  if (!UpdateEnd) {
    GP.BLOCK_BEGIN(GP_THIN, "", "–ó–∞–≥—Ä—É–∑–∫–∞ –ø—Ä–æ—à–∏–≤–∫–∏", UI_BLOCK_COLOR);
    GP.SPAN("–ü—Ä–æ—à–∏–≤–∫—É –º–æ–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å –≤ Arduino IDE: –°–∫–µ—Ç—á -> –≠–∫—Å–ø–æ—Ä—Ç –±–∏–Ω–∞—Ä–Ω–æ–≥–æ —Ñ–∞–π–ª–∞ (—Å–æ—Ö—Ä–∞–Ω—è–µ—Ç—Å—è –≤ –ø–∞–ø–∫—É —Å –ø—Ä–æ—à–∏–≤–∫–æ–π).", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP.SPAN("–ü–æ–¥–¥–µ—Ä–∂–∏–≤–∞–µ–º—ã–µ —Ñ–æ—Ä–º–∞—Ç—ã —Ñ–∞–π–ª–æ–≤: bin –∏ bin.gz.", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP.HR_TEXT("–ó–∞–≥—Ä—É–∑–∏—Ç—å —Ñ–∞–π–ª—ã", UI_LINE_COLOR, UI_HINT_COLOR);
    M_BOX(GP.LABEL("–ü—Ä–æ—à–∏–≤–∫–∞ ESP", "", UI_LABEL_COLOR); GP.OTA_FIRMWARE("üì•", UI_BUTTON_COLOR, true););
    GP.VOID_BOX("0;height:10px");
  }
  else {
    GP.BLOCK_BEGIN(GP_THIN, "", "–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏", UI_BLOCK_COLOR);
    GP.BLOCK_OFFSET_BEGIN();
    if (UpdateError.length()) {
      GP.SPAN("<big><b>–ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–∏...</b></big><br><small>[" + UpdateError + "]</small>", GP_CENTER, "", GP_RED); //–æ–ø–∏—Å–∞–Ω–∏–µ
    }
    else {
      GP.SPAN("<big><b>–í—ã–ø–æ–ª–Ω—è–µ—Ç—Å—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏...</b></big>", GP_CENTER, "syncUpdate", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
      GP.SPAN("<small>–ù–µ –≤—ã–∫–ª—é—á–∞–π—Ç–µ —É—Å—Ç—Ä–æ–π—Å—Ç–≤–æ –¥–æ –∑–∞–≤–µ—Ä—à–µ–Ω–∏—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è!</small>", GP_CENTER, "syncWarn", GP_RED); //–æ–ø–∏—Å–∞–Ω–∏–µ
      GP.UPDATE("syncUpdate,syncWarn");
      setUpdateCompleted();
    }
    GP.BLOCK_END();
  }
  GP.HR(UI_LINE_COLOR);
  GP.BOX_BEGIN(GP_CENTER);
  GP.BUTTON_MINI_LINK("/", "–í–µ—Ä–Ω—É—Ç—å—Å—è –Ω–∞ –≥–ª–∞–≤–Ω—É—é", UI_BUTTON_COLOR);
  GP.BOX_END();
  GP.BLOCK_END();

  GP.PAGE_BLOCK_END();
  GP.BUILD_END();
}
//--------------------------------------------------------------------
void action() {
  if (ui.click()) {
    if (ui.clickSub("ext")) {
      if (ui.clickSub("extSendDel")) {
        for (uint8_t multiNum = constrain(ui.clickNameSub(1).toInt(), 0, (MAX_CLOCK - 1)); multiNum < (MAX_CLOCK - 1); multiNum++) {
          settings.send[multiNum] = settings.send[multiNum + 1]; //—Å–º–µ—â–∞–µ–º –∞–¥—Ä–µ—Å–∞
          settings.attempt[multiNum] = settings.attempt[multiNum + 1]; //—Å–º–µ—â–∞–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –ø–æ–ø—ã—Ç–æ–∫
        }
        settings.send[MAX_CLOCK - 1] = IPAddress(0, 0, 0, 0); //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –∞–¥—Ä–µ—Å –æ—Ç–ø—Ä–∞–≤–∫–∏
        settings.attempt[MAX_CLOCK - 1] = 1; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ–ø—ã—Ç–∫–∏ –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é
        memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
      if (ui.click("extSendAdd")) {
        if (buffSendIp[0] != '\0') { //–µ—Å–ª–∏ —Å—Ç—Ä–æ–∫–∞ –Ω–µ –ø—É—Å—Ç–∞—è
          IPAddress _send_ip; //ip –∞–¥—Ä–µ—Å –Ω–æ–≤–æ–≥–æ —Ö–æ—Å—Ç–∞
          if (_send_ip.fromString(buffSendIp)) { //–µ—Å–ª–∏ –∞–¥—Ä–µ—Å –¥–µ–π—Å—Ç–≤–∏—Ç–µ–ª—å–Ω—ã–π
            if (WiFi.localIP() != _send_ip) { //–µ—Å–ª–∏ –Ω–µ —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã–π –∞–¥—Ä–µ—Å
              for (uint8_t i = 0; i < MAX_CLOCK; i++) {
                if (!settings.send[i]) { //–µ—Å–ª–∏ —è—á–µ–π–∫–∞ –Ω–µ –∑–∞–ø–æ–ª–Ω–µ–Ω–∞
                  sendHostNum = i; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —Ç–µ–∫—É—â–∏–π —Ö–æ—Å—Ç
                  settings.send[i] = _send_ip; //–∫–æ–ø–∏—Ä—É–µ–º –∞–¥—Ä–µ—Å
                  settings.attempt[i] = constrain(buffSendAttempt, 1, 5); //–∫–æ–ø–∏—Ä—É–µ–º –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –ø–æ–ø—ã—Ç–æ–∫
                  memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
                  break;
                }
                else if (settings.send[i] == _send_ip) break;
              }
            }
          }
        }
      }

      if (ui.click("extSendIp")) {
        strncpy(buffSendIp, ui.getString("extSendIp").c_str(), 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
        buffSendIp[19] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      }
      if (ui.click("extSendAttempt")) {
        buffSendAttempt = constrain(ui.getInt("extSendAttempt"), 1, 5); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
      }

      if (ui.click("extClear")) {
        settings.ssid[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        settings.pass[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
      if (ui.click("extScan")) {
        if (wifiGetScanAllowStatus()) wifiStartScanNetworks(); //–Ω–∞—á–∏–Ω–∞–µ–º –ø–æ–∏—Å–∫
      }

      if (ui.click("extPeriod")) {
        settings.period = ui.getInt("extPeriod");
        memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
    }
  }
  /**************************************************************************/
  if (ui.form()) {
    if (!wifiGetConnectWaitStatus() && !wifiGetConnectStatus()) {
      if (ui.form("/connection")) {
        wifiSetConnectStatus(); //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –∏–Ω—Ç–µ—Ä–≤–∞–ª –ø–µ—Ä–µ–ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è
        String _ssid; //–≤—Ä–µ–º–µ–Ω–Ω–∞—è —Å—Ç—Ä–æ–∫–∞ ssid —Å–µ—Ç–∏
        _ssid.reserve(64); //—Ä–µ–∑–µ—Ä–≤–∏—Ä—É–µ–º –≤—Å—é –¥–ª–∏–Ω–Ω—É
        if (ui.copyString("wifiSsid", _ssid)) { //–∫–æ–ø–∏—Ä—É–µ–º ssid —Å–µ—Ç–∏
          _ssid.replace(" üîí", ""); //—É–¥–∞–ª—è–µ–º –ª–∏—à–Ω–∏–µ —Å–∏–º–≤–æ–ª—ã
          _ssid.trim(); //—É–¥–∞–ª—è–µ–º –ø—Ä–æ–±–µ–ª—ã
          strncpy(settings.ssid, _ssid.c_str(), 64); //–∫–æ–ø–∏—Ä—É–µ–º ssid —Å–µ—Ç–∏
        }
        else wifiResetConnectStatus(); //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –∏–Ω—Ç–µ—Ä–≤–∞–ª –ø–µ—Ä–µ–ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è
        settings.ssid[63] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        ui.copyStr("wifiPass", settings.pass, 64); //–∫–æ–ø–∏—Ä—É–µ–º –ø–∞—Ä–æ–ª—å —Å–µ—Ç–∏
        settings.pass[63] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
    }
    else if (ui.form("/network")) {
      wifiResetConnectStatus(); //–æ—Ç–∫–ª—é—á–∞–µ–º—Å—è –æ—Ç —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞
      settings.ssid[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      settings.pass[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
    }
  }
  /**************************************************************************/
  if (ui.update()) {
    if (ui.updateSub("ext")) {
      if (ui.update("extNetwork")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(wifiGetConnectState());
      }
      if (ui.update("extConnect") && wifiGetConnectStatus()) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(1);
      }
      if (ui.update("extScan") && wifiGetScanCompleteStatus()) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(1);
        wifiResetScanCompleteStatus();
      }
    }
    //--------------------------------------------------------------------
    if (ui.updateSub("inf")) {
      if (ui.update("infSignal")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(stringGetPercent(wifiGetSignal()));
      }
      if (ui.update("infPower")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(stringGetVoltage(getBatteryVoltage()));
      }
      if (ui.update("infUsage")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(stringGetPercent(systemGetUsage()));
      }
      if (ui.update("infUptime")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(stringGetTimeFromMs(millis()));
      }
    }
    //--------------------------------------------------------------------
    if (ui.updateSub("bar")) {
      if (ui.update("barBat")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(getBatteryState());
      }

      if (ui.update("barTemp")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.temp / 10.0, 1) + "¬∞–°");
      }
      if (ui.update("barHum")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.hum) + "%");
      }
      if (ui.update("barPress")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.press) + "mm.Hg");
      }
    }
  }
}
//--------------------------------------------------------------------
String stringLengthConstrain(String str, uint8_t size) {
  if (str.length() > size) {
    str.remove(size);
    str += F("‚Ä¶");
  }
  return str;
}
//--------------------------------------------------------------------
String stringGetPercent(uint8_t num) {
  String str;
  str.reserve(10);
  str = num;
  str += '%';
  return str;
}
//--------------------------------------------------------------------
String stringGetVoltage(float num) {
  String str;
  str.reserve(10);
  str = String(num, 2);
  str += F(" V");
  return str;
}
//--------------------------------------------------------------------
String stringGetFreq(uint32_t num) {
  String str;
  str.reserve(15);
  str = num;
  str += F(" MHz");
  return str;
}
//--------------------------------------------------------------------
String stringGetHex(uint32_t num) {
  String str;
  str.reserve(15);
  str = String(num, HEX);
  str.toUpperCase();
  str = "0x" + str;
  return str;
}
//--------------------------------------------------------------------
String stringGetKilobyte(uint32_t num, uint8_t dec) {
  String str;
  str.reserve(15);
  str = String(num / 1000.0, dec);
  str += F(" kB");
  return str;
}
//--------------------------------------------------------------------
String stringGetTimeFromMs(uint32_t data) {
  data /= 1000;

  uint8_t second = data % 60;
  data /= 60;
  uint8_t minute = data % 60;
  data /= 60;
  uint16_t hour = data % 24;
  data /= 24;

  String str;
  str.reserve(20);

  str = data;
  str += ':';
  str += hour;
  str += ':';
  str += minute / 10;
  str += minute % 10;
  str += ':';
  str += second / 10;
  str += second % 10;

  return str;
}
//--------------------------------------------------------------------
float getBatteryVoltage(void) { //–ø–æ–ª—É—á–∏—Ç—å –Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  return (vccVoltage / 1000.0);
}
//--------------------------------------------------------------------
uint8_t getBatteryCharge(void) { //–ø–æ–ª—É—á–∏—Ç—å —É—Ä–æ–≤–µ–Ω—å –∑–∞—Ä—è–¥–∞ –±–∞—Ç–∞—Ä–µ–∏
  if (vccVoltage <= BAT_VOLTAGE_MIN) return 0;
  return constrain((uint8_t)(((vccVoltage - BAT_VOLTAGE_MIN) / ((BAT_VOLTAGE_MAX - BAT_VOLTAGE_MIN) / 20.0)) + 1) * 5, 5, 100);
}
//--------------------------------------------------------------------
String getBatteryState(void) { //–ø–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  String data = "üîã ";
  data += getBatteryCharge();
  data += '%';
  return data;
}
//--------------------------------------------------------------------
char getHexChar(uint8_t hex) {
  if (hex > 15) return 'F';
  if (hex > 9) return ('A' + (hex - 10));
  return ('0' + hex);
}
//--------------------------------------------------------------------
String getSensorId(void) {
  String str;
  str.reserve(20);
  str = "";

  uint8_t mac[6];
  efuseGetDefaultMacAddress(mac);

  for (uint8_t i = 0; i < 6; i++) {
    str += getHexChar(mac[i] >> 4);
    str += getHexChar(mac[i] & 0x0F);
    if (i < 5) str += ':';
  }

  return str;
}
//--------------------------------------------------------------------
void efuseGetDefaultMacAddress(uint8_t* mac) {
  uint32_t _efuse_low = REG_READ(0x3FF00050);
  uint32_t _efuse_mid = REG_READ(0x3FF00054);
  uint32_t _efuse_high = REG_READ(0x3FF0005C);

  mac[0] = _efuse_high >> 16;
  mac[1] = _efuse_high >> 8;
  mac[2] = _efuse_high;
  mac[3] = _efuse_mid >> 8;
  mac[4] = _efuse_mid;
  mac[5] = _efuse_low >> 24;
}
//--------------------------------------------------------------------
void checkSettingsButton(void) {
  if (ESP.rtcUserMemoryRead(32, rtcMemory, sizeof(rtcMemory))) {
    if ((rtcMemory[0] ^ rtcMemory[1]) != 0xFFFFFFFF) {
      rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = 0xFFFFFFFF; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
      rtcMemory[2] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ wifi
      rtcMemory[3] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ wifi
      rtcMemory[4] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ dhcp
      rtcMemory[5] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ dhcp
      rtcMemory[6] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ dhcp
      rtcMemory[7] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ dns
      rtcMemory[8] = 0x00; //—Å–±—Ä–æ—Å–∏–ª–∏ –±–ª–æ–∫ –Ω–∞—Å—Ç—Ä–æ–µ–∫ dns

      sensorStartWait = 2;
#if DEBUG_MODE
      Serial.println F("Settings button reset!");
#endif
    }
    else if (rtcMemory[0] < 64) {
      rtcMemory[0]++; //–¥–æ–±–∞–≤–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏–µ –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = rtcMemory[0] ^ 0xFFFFFFFF; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
#if DEBUG_MODE
      Serial.print F("Settings button click ");
      Serial.print(rtcMemory[0]);
      Serial.println F("...");
#endif
    }
    if (rtcMemory[0] >= 3) { //–µ—Å–ª–∏ –±—ã–ª–æ –¥–æ—Å—Ç–∞—Ç–æ—á–Ω–æ–µ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –Ω–∞–∂–∞—Ç–∏–π
      rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = 0xFFFFFFFF; //—Å–±—Ä–æ—Å–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
      settingsMode = true; //–≤–∫–ª—é—á–∏—Ç—å —Ä–µ–∂–∏–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
#if DEBUG_MODE
      Serial.println F("Settings mode enable...");
#endif
    }
  }
  if (ESP.rtcUserMemoryWrite(32, rtcMemory, sizeof(rtcMemory))) {
#if DEBUG_MODE
    Serial.println F("Settings button update!");
#endif
  }
}
//--------------------------------------------------------------------
void resetSettingsButton(void) {
  if (rtcMemory[0]) { //–µ—Å–ª–∏ –∫–Ω–æ–ø–∫–∞ –±—ã–ª–∞ –Ω–∞–∂–∞—Ç–∞
    rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
    rtcMemory[1] = 0xFFFFFFFF; //—Å–±—Ä–æ—Å–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
    if (ESP.rtcUserMemoryWrite(32, rtcMemory, sizeof(rtcMemory))) {
#if DEBUG_MODE
      Serial.println F("Settings button reset!");
#endif
    }
  }
}
//--------------------------------------------------------------------
void setUpdateCompleted(void) {
  rtcMemory[0] = 64; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —Ñ–ª–∞–≥ –∑–∞–≤–µ—Ä—à–µ–Ω–∏—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è
  rtcMemory[1] = rtcMemory[0] ^ 0xFFFFFFFF; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
  if (ESP.rtcUserMemoryWrite(32, rtcMemory, sizeof(rtcMemory))) {
#if DEBUG_MODE
    Serial.println F("Firmware mark update!");
#endif
  }
}
//--------------------------------------------------------------------
void sleepMode(void) {
#if DEBUG_MODE
#if SLEEP_MODE
  Serial.print F("Sleep mode, wake after ");
#else
  Serial.print F("Wait mode, wake after ");
#endif
  Serial.print(sleepTime[settings.period]);
  Serial.println F(" min...");
#endif
#if STATUS_LED > 0
  if (settingsMode == true) digitalWrite(LED_BUILTIN, HIGH); //–≤—ã–∫–ª—é—á–∞–µ–º –∏–Ω–¥–∏–∫–∞—Ü–∏—é
#endif

  ui.stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—å ui
  udp.stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—å udp

  WiFi.disconnect(); //–æ—Ç–∫–ª—é—á–∞–µ–º—Å—è –æ—Ç —Å–µ—Ç–∏
  WiFi.mode(WIFI_OFF); //–æ—Ç–∫–ª—é—á–∞–µ–º wifi

#if SLEEP_MODE
  ESP.deepSleep((60e6 * sleepTime[settings.period]) - (millis() * 1000)); //—É—Ö–æ–¥–∏–º –≤ —Å–æ–Ω
#else
  WiFi.forceSleepBegin(); //–≤—ã–∫–ª—é—á–∞–µ–º –ø–∏—Ç–∞–Ω–∏–µ wifi
  delay((60e3 * sleepTime[settings.period]) - millis()); //–∂–¥–µ–º –æ–∫–æ–Ω—á–∞–Ω–∏—è —Å–Ω–∞
  ESP.restart(); //–ø–µ—Ä–µ–∑–∞–≥—Ä—É–∑–∫–∞
#endif
}
//--------------------------------------------------------------------
void lowBattery(void) {
#if DEBUG_MODE
  Serial.println F("Battery low, power down...");
#endif
#if STATUS_LED > 0
  for (uint8_t i = 0; i < 5; i++) {
    digitalWrite(LED_BUILTIN, (boolean)(i & 0x01));
    delay(200);
  }
  digitalWrite(LED_BUILTIN, HIGH);
#endif
  resetSettingsButton(); //—Å–±—Ä–æ—Å–∏—Ç—å –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏ –Ω–∞—Å—Ç—Ä–æ–µ–∫
  ESP.deepSleep(0); //—É—Ö–æ–¥–∏–º –≤ —Å–æ–Ω
}
//--------------------------------------------------------------------
void checkBattery(void) {
  vccVoltage = ESP.getVcc();
  if (vccVoltage > BAT_VOLTAGE_CORRECT) vccVoltage -= BAT_VOLTAGE_CORRECT;
  if (!getBatteryCharge()) lowBattery();
}
//--------------------------------------------------------------------
void checkSettings(void) {
  if (settings.ssid[0] == '\0') {
    delay(1000);
    resetSettingsButton(); //—Å–±—Ä–æ—Å–∏—Ç—å –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏ –Ω–∞—Å—Ç—Ä–æ–µ–∫
#if DEBUG_MODE
    Serial.println F("Wifi is not configured, power down...");
#endif
#if STATUS_LED > 0
    for (uint8_t i = 0; i < 3; i++) {
      digitalWrite(LED_BUILTIN, (boolean)(i & 0x01));
      delay(500);
    }
    digitalWrite(LED_BUILTIN, HIGH);
#endif
    ESP.deepSleep(0); //—É—Ö–æ–¥–∏–º –≤ —Å–æ–Ω
  }
}
//--------------------------------------------------------------------
void checkSensors(void) {
#if DEBUG_MODE
  Serial.println F("Sensor check start...");
#endif

  if (settingsMode == true) settings.sensor = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–π–¥–µ–Ω–Ω—ã–µ —Å–µ–Ω—Å–æ—Ä—ã

  sens.temp = 0x7FFF; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—É
  sens.press = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –¥–∞–≤–ª–µ–Ω–∏–µ
  sens.hum = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –≤–ª–∞–∂–Ω–æ—Å—Ç—å

  sensorReady = false; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ñ–ª–∞–≥ –≥–æ—Ç–æ–≤–Ω–æ—Å—Ç–∏ –∑–∞–º–µ—Ä–∞

  if (!settings.sensor) {
    if (!readTempDHT()) {
      if (!readTempDS()) {
        readTempBME();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
        readTempSHT();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
        readTempAHT();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
      }
    }
    if (settings.sensor) {
      memoryWriteSettings(); //–∑–∞–ø–∏—Å–∞—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç—å
#if DEBUG_MODE
      Serial.println F("Sensor found, update memory...");
#endif
    }
    else {
#if DEBUG_MODE
      Serial.print F("Sensor not found");
#endif
      if (settingsMode == true) {
        memorySaveSettings(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
#if DEBUG_MODE
        Serial.println F(", update memory...");
#endif
      }
#if DEBUG_MODE
      else Serial.println F("!");
#endif
    }
  }
  else {
    for (uint8_t sensor = 0; sensor < SENS_MAX; sensor++) {
      if (settings.sensor & (0x01 << sensor)) {
        switch (sensor) {
          case SENS_DHT: readTempDHT(); return;
          case SENS_DS: readTempDS(); return;
          case SENS_BME: readTempBME(); twi_write_stop(); break;
          case SENS_SHT: readTempSHT(); twi_write_stop(); break;
          case SENS_AHT: readTempAHT(); twi_write_stop(); break;
        }
      }
    }
  }
}
//--------------------------------------------------------------------
void updateSensors(void) {
  updateTempDS(); //–∑–∞–±—Ä–∞—Ç—å —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—É DS18B20

  if (sens.temp != 0x7FFF) { //–µ—Å–ª–∏ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞ –¥–æ—Å—Ç—É–ø–Ω–∞
    if (sens.temp > 850) sens.temp = 850; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª
    if (sens.temp < -850) sens.temp = -850; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª
  }
  if (sens.hum > 99) sens.hum = 99; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª

  sensorReady = true; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —Ñ–ª–∞–≥ –≥–æ—Ç–æ–≤–Ω–æ—Å—Ç–∏ –∑–∞–º–µ—Ä–∞

#if DEBUG_MODE
  Serial.println F("Sensor check end...");
#endif
}
//--------------------------------------------------------------------
void updateBuffer(void) {
  static boolean _init = false; //—Ñ–ª–∞–≥ –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏ –¥–∞–Ω–Ω—ã—Ö

  if (_init != true) { //–µ—Å–ª–∏ –¥–∞–Ω–Ω—ã–µ –Ω–µ —Å—Ñ–æ—Ä–º–∏—Ä–æ–≤–∞–Ω—ã
    _init = true; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º —Ñ–ª–∞–≥ –∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏–∏ –¥–∞–Ω–Ω—ã—Ö

    efuseGetDefaultMacAddress(buffSendData); //–ø–æ–ª—É—á–∏—Ç—å mac –∞–¥—Ä–µ—Å

    if (settingsMode == true) buffSendData[6] = UDP_FOUND_CMD;
    else if (settings.send[0]) buffSendData[6] = UDP_ANSWER_CMD;
    else buffSendData[6] = UDP_WRITE_CMD;

    buffSendData[7] = (uint8_t)sens.temp;
    buffSendData[8] = (uint8_t)(sens.temp >> 8);

    buffSendData[9] = (uint8_t)sens.press;
    buffSendData[10] = (uint8_t)(sens.press >> 8);

    buffSendData[11] = (uint8_t)sens.hum;

    buffSendData[12] = (uint8_t)getBatteryCharge();
    buffSendData[13] = (uint8_t)wifiGetSignal();
    buffSendData[14] = (uint8_t)sleepTime[settings.period];

    uint8_t crc = 0;
    for (uint8_t i = 0; i < (UDP_SEND_SIZE - 1); i++) checkCRC(&crc, buffSendData[i]);

    buffSendData[15] = (uint8_t)crc;

#if DEBUG_MODE
    Serial.println F("Send buffer update...");
#endif
  }
}
//--------------------------------------------------------------------
boolean sendCheck(void) {
  if (sendAnswerWait) {
    if (udp.parsePacket() == UDP_ANSWER_SIZE) {
      if (udp.remotePort() == UDP_CLOCK_PORT) {
        if (udp.remoteIP() == settings.send[sendHostNum]) {
          if (udp.read() == UDP_ANSWER_CMD) {
#if DEBUG_MODE
            Serial.println F("Send answer ok...");
#endif
            sendHostAttempt = 0;
            sendHostNum++;
          }
#if DEBUG_MODE
          else {
            Serial.println F("Send answer error!");
          }
#endif
          sendAnswerWait = false;
        }
      }
    }
    else if ((millis() - sendAnswerTimer) >= UDP_ANSWER_WAIT_TIME) {
#if DEBUG_MODE
      Serial.println F("Send answer timeout!");
#endif
      sendAnswerWait = false;
    }
  }
  return !sendAnswerWait;
}
//--------------------------------------------------------------------
void sendReset(void) {
#if DEBUG_MODE
  Serial.println F("Send wait answer...");
#endif
  udp.flush();
  sendAnswerWait = true;
  sendAnswerTimer = millis();
}
//--------------------------------------------------------------------
void sendUpdate(void) {
  if (wifiGetConnectStatus() && pingCheck() && sendCheck() && (sensorReady == true)) {
    if (sendHostNum < MAX_CLOCK) {
      if (settings.send[sendHostNum] || !sendHostNum) {
        updateBuffer(); //–æ–±–Ω–æ–≤–∏—Ç—å –±—É—Ñ–µ—Ä –æ—Ç–ø—Ä–∞–≤–∫–∏
#if DEBUG_MODE
        Serial.print F("Send package to IP address: ");
        if (settings.send[sendHostNum]) {
          IPAddress addr = settings.send[sendHostNum];
          Serial.println(addr.toString());
        }
        else Serial.println(wifiGetBroadcastIP().toString());
#endif
        if (!udp.beginPacket((settings.send[sendHostNum]) ? settings.send[sendHostNum] : wifiGetBroadcastIP(), UDP_CLOCK_PORT) || (udp.write(buffSendData, UDP_SEND_SIZE) != UDP_SEND_SIZE) || !udp.endPacket()) {
#if DEBUG_MODE
          Serial.println F("Send package fail!");
#endif
        }
#if DEBUG_MODE
        else {
          Serial.println F("Send package ok...");
        }
#endif
        if (buffSendData[6] == UDP_ANSWER_CMD) {
          if (++sendHostAttempt >= settings.attempt[sendHostNum]) {
            sendHostAttempt = 0;
            sendHostNum++;
          }
          sendReset();
        }
        else sendHostNum++;
      }
      else {
        sendHostNum = MAX_CLOCK;
        if (buffSendData[6] != UDP_ANSWER_CMD) pingReset();
#if DEBUG_MODE
        Serial.println F("Send all packages completed...");
#endif
      }
    }
    else if (settingsMode == false) sleepMode(); //–æ—Ç–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ
  }
}
//--------------------------------------------------------------------
void timeUpdate(void) {
  static uint8_t updateTimer = 0;
  static uint32_t secondsTimer = 0;

  if (!secondsTimer || ((millis() - secondsTimer) >= 1000)) {
    secondsTimer = millis();

    if (updateTimer == sensorStartWait) checkSensors(); //–ø—Ä–æ–≤–µ—Ä–∫–∞ –¥–æ—Å—Ç—É–ø–Ω–æ—Å—Ç–∏ —Å–µ–Ω—Å–æ—Ä–æ–≤
    else if (updateTimer == (sensorStartWait + 1)) updateSensors(); //–æ–±–Ω–æ–≤–∏—Ç—å –ø–æ–∫–∞–∑–∞–Ω–∏—è —Å–µ–Ω—Å–æ—Ä–æ–≤
    else if ((updateTimer > (SETTINGS_MODE_TIME - 15)) && (settingsMode == true) && ui.online()) updateTimer = (SETTINGS_MODE_TIME - 15); //—Å–±—Ä–æ—Å–∏—Ç—å —Ç–∞–π–º–µ—Ä
    else if (updateTimer > ((settingsMode == false) ? 15 : SETTINGS_MODE_TIME)) sleepMode(); //–æ—Ç–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ

    if (updateTimer < 255) updateTimer++; //–ø—Ä–∏–±–∞–≤–∏–ª–∏ —Ç–∞–π–º–µ—Ä —Å–µ–∫—É–Ω–¥

    if (updateTimer > 1) {
      resetSettingsButton(); //—Å–±—Ä–æ—Å–∏—Ç—å –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏ –Ω–∞—Å—Ç—Ä–æ–µ–∫
#if STATUS_LED > 0
      if ((settingsMode == true) && wifiGetConnectWaitStatus()) digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN)); //–º–∏–≥–∞–µ–º –∏–Ω–¥–∏–∫–∞—Ü–∏–µ–π
#endif
    }
  }
}
//--------------------------------------------------------------------
void systemUpdate(void) {
  ui.tick(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –≤–µ–± –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å–∞
  if (sysCycleCount != 0xFFFFFFFF) sysCycleCount++; //–ø–æ—Å—á–µ—Ç —Ü–∏–∫–ª–æ–≤
}
//--------------------------------------------------------------------
uint8_t systemGetUsage(void) {
  static uint32_t timer = 0;
  static uint8_t usage = 0;

  uint32_t ms = millis();
  if ((ms - timer) >= 1000) {
    usage = map(constrain(sysCycleCount / (ms - timer), 1, 25), 1, 25, 100, 0);
    sysCycleCount = 0;
    timer = ms;
  }
  return usage;
}
//--------------------------------------------------------------------
void setup() {
  //–≤—ã–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ wifi
  WiFi.forceSleepBegin();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –∏–Ω–¥–∏–∫–∞—Ç–æ—Ä–∞
#if STATUS_LED > 0
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, HIGH);
#endif

  //–æ–±–Ω–æ–≤–∏—Ç—å –Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è
  checkBattery();

#if DEBUG_MODE
  Serial.begin(115200);
  Serial.println F("");
  Serial.println F("Startup...");
  Serial.print F("Supply voltage ");
  Serial.print(getBatteryVoltage(), 2);
  Serial.println F("...");
  Serial.print F("Firmware version ");
  Serial.print F(ESP_FIRMWARE_VERSION);
  Serial.println F("...");
#endif

  //–ø—Ä–æ–≤–µ—Ä–∫–∞ –∫–Ω–æ–ø–∫–∏ —Ä–µ–∂–∏–º–∞ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
  checkSettingsButton();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –∏–Ω–¥–∏–∫–∞—Ç–æ—Ä–∞
#if STATUS_LED > 0
  if (settingsMode == true) digitalWrite(LED_BUILTIN, LOW);
#endif

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è —à–∏–Ω—ã
  pinMode(TWI_SDA_PIN, INPUT_PULLUP);
  pinMode(TWI_SCL_PIN, INPUT_PULLUP);
  twi_init();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –ø–æ OTA
  if (settingsMode == true) {
    if ((getBatteryCharge() < 50) || (ESP.getFreeSketchSpace() < ESP.getSketchSize())) {
      otaUpdate = false; //–≤—ã–∫–ª—é—á–∞–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
#if DEBUG_MODE
      Serial.println F("OTA update disable...");
#endif
    }
#if DEBUG_MODE
    else Serial.println F("OTA update enable...");
#endif
  }

  //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–µ—Ä–∏–æ–¥ –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é
  settings.period = SEND_DATA_PERIOD;

  //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ –æ—Ç–ø—Ä–∞–≤–∫–∏ –¥–∞–Ω–Ω—ã—Ö
  for (uint8_t i = 0; i < MAX_CLOCK; i++) {
    settings.send[i] = IPAddress(0, 0, 0, 0);
    settings.attempt[i] = 1;
  }

  //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ—Ç–∏
  settings.ssid[0] = '\0';
  settings.pass[0] = '\0';

  //—á–∏—Ç–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ –∏–∑ –ø–∞–º—è—Ç–∏
  memoryReadSettings();

  //–ø—Ä–æ–≤–µ—Ä—è–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ—Ç–∏
  if (settingsMode == false) checkSettings();

  //–ø–æ–¥–∫–ª—é—á–∞–µ–º –∫–æ–Ω—Å—Ç—Ä—É–∫—Ç–æ—Ä –∏ –∑–∞–ø—É—Å–∫–∞–µ–º –≤–µ–± –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å
  if (settingsMode == true) {
    ui.attachBuild(build);
    ui.attach(action);
    ui.start();

    //–Ω–∞—Å—Ç—Ä–∞–∏–≤–∞–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –±–µ–∑ –ø–∞—Ä–æ–ª—è
    if (otaUpdate) {
      ui.enableOTA();
      ui.OTA.attachUpdateBuild(buildUpdate);
    }
    ui.downloadAuto(true);
    ui.uploadAuto(false);
  }

  //–≤–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ wifi
  WiFi.forceSleepWake();

  //–Ω–∞—Å—Ç—Ä–∞–∏–≤–∞–µ–º wifi
  WiFi.persistent(false);
  if (WiFi.getAutoConnect() != false) WiFi.setAutoConnect(false);
  if (WiFi.getAutoReconnect() != false) WiFi.setAutoReconnect(false);
  if (settingsMode == true) wifiStartAP();
  else wifiStartSTA();
}
//--------------------------------------------------------------------
void loop() {
  if (wifiUpdate()) sleepMode(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ —Å—Ç–∞—Ç—É—Å–æ–≤ wifi

  timeUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –≤—Ä–µ–º–µ–Ω–∏
  sendUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –∫–∞–Ω–∞–ª–∞ —Å–≤—è–∑–∏

  if (settingsMode == true) {
    memoryUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –ø–∞–º—è—Ç–∏ –Ω–∞—Å—Ç—Ä–æ–µ–∫
    systemUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ —Å–∏—Å—Ç–µ–º–Ω—ã—Ö —Ñ—É–Ω–∫—Ü–∏–π
  }
}
