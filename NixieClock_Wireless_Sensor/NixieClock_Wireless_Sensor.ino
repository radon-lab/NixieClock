/*
  Arduino IDE 1.8.13 –≤–µ—Ä—Å–∏—è –ø—Ä–æ—à–∏–≤–∫–∏ 1.1.4 —Ä–µ–ª–∏–∑ –æ—Ç 23.10.24
  –°–ø–µ—Ü–∏–ª—å–Ω–æ –¥–ª—è –ø—Ä–æ–µ–∫—Ç–∞ "–ß–∞—Å—ã –Ω–∞ –ì–†–ò v2. –ê–ª—å—Ç–µ—Ä–Ω–∞—Ç–∏–≤–Ω–∞—è –ø—Ä–æ—à–∏–≤–∫–∞"
  –°—Ç—Ä–∞–Ω–∏—Ü–∞ –ø—Ä–æ–µ–∫—Ç–∞ - https://community.alexgyver.ru/threads/chasy-na-gri-v2-alternativnaja-proshivka.5843/

  –ò—Å—Ö–æ–¥–Ω–∏–∫ - https://github.com/radon-lab/NixieClock
  –ê–≤—Ç–æ—Ä Radon-lab

  –ï—Å–ª–∏ –Ω–µ —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–æ —è–¥—Ä–æ ESP8266, "–§–∞–π–ª -> –ù–∞—Å—Ç—Ä–æ–π–∫–∏ -> –î–æ–ø–æ–ª–Ω–∏—Ç–µ–ª—å–Ω—ã–µ —Å—Å—ã–ª–∫–∏ –¥–ª—è –ú–µ–Ω–µ–¥–∂–µ—Ä–∞ –ø–ª–∞—Ç", –≤ –æ–∫–Ω–æ –≤–≤–æ–¥–∞ –≤—Å—Ç–∞–≤–ª—è–µ—Ç–µ —Å—Å—ã–ª–∫—É - https://arduino.esp8266.com/stable/package_esp8266com_index.json
  –î–∞–ª–µ–µ "–ò–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã -> –ü–ª–∞—Ç–∞ -> –ú–µ–Ω–µ–¥–∂–µ—Ä –ø–ª–∞—Ç..." –Ω–∞—Ö–æ–¥–∏—Ç–µ –ø–ª–∞—Ç—É esp8266 –∏ —É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ—Ç–µ –≤–µ—Ä—Å–∏—é 2.7.4!

  –í "–ò–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã -> –£–ø—Ä–∞–≤–ª—è—Ç—å –±–∏–±–ª–∏–æ—Ç–µ–∫–∞–º–∏..." –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ –ø—Ä–µ–¥–≤–∞—Ä–∏—Ç–µ–ª—å–Ω–æ —É—Å—Ç–∞–Ω–æ–≤–∏—Ç—å —É–∫–∞–∑–∞–Ω–Ω—ã–µ –≤–µ—Ä—Å–∏–∏ –±–∏–±–ª–∏–æ—Ç–µ–∫:
  GyverPortal 3.6.6
  EEManager 2.0.1

  –í "–ò–Ω—Å—Ç—Ä—É–º–µ–Ω—Ç—ã -> Flash Size" –Ω–µ–æ–±—Ö–æ–¥–∏–º–æ –≤—ã–±—Ä–∞—Ç—å —Ä–∞—Å–ø—Ä–µ–¥–µ–ª–µ–Ω–∏–µ –ø–∞–º—è—Ç–∏ –≤ –∑–∞–≤–∏—Å–∏–º–æ—Å—Ç–∏ –æ—Ç —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–Ω–æ–≥–æ –æ–±—ä—ë–º–∞ FLASH:
  1–ú–ë - FS:64KB OTA:~470KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  2–ú–ë - FS:1MB OTA:~512KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  4–ú–ë - FS:2MB OTA:~1019KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
  8–ú–ë - FS:6MB OTA:~1019KB(–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ esp –ø–æ OTA).
*/
#include "config.h"

#define GP_NO_DNS
#define GP_NO_MDNS

#include <GyverPortal.h>
GyverPortal ui;

struct settingsData {
  uint8_t sensor;
  uint8_t period;
  char ssid[64];
  char pass[64];
  char send[MAX_CLOCK * 2][20];
} settings;

#include <EEManager.h>
EEManager memory(settings, 3000);

#include <WiFiUdp.h>
WiFiUDP udp;

//–ø–µ—Ä–µ–º–µ–Ω–Ω—ã–µ
char buffSendIp[20]; //–±—É—Ñ–µ—Ä ip –∞–¥—Ä–µ—Å–∞
char buffSendName[20]; //–±—É—Ñ–µ—Ä –∏–º–µ–Ω–∏
uint8_t buffSendData[UDP_SEND_SIZE]; //–±—É—Ñ–µ—Ä –æ—Ç–ø—Ä–∞–≤–∫–∏

uint32_t rtcMemory[2]; //–ø–∞–º—è—Ç—å –Ω–∞–∂–∞—Ç–∏–π –∫–Ω–æ–ø–∫–∏ —Å–±—Ä–æ—Å–∞

uint8_t sendHostNum = 0; //—Ç–µ–∫—É—â–∏–π –Ω–æ–º–µ—Ä —Ö–æ—Å—Ç–∞

boolean otaUpdate = true; //—Ñ–ª–∞–≥ –∑–∞–ø—Ä–µ—Ç–∞ –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –µ—Å–ø
boolean sendReady = false; //—Ñ–ª–∞–≥ –ø–æ–≤—Ç–æ—Ä–Ω–æ–π –ø–æ–ø—ã—Ç–∫–∏ –æ—Ç–ø—Ä–∞–≤–∫–∏ –¥–∞–Ω–Ω—ã—Ö
boolean sensorReady = false; //—Ñ–ª–∞–≥ –æ–∫–æ–Ω—á–∞–Ω–∏—è –∑–∞–º–µ—Ä–∞ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—ã
boolean settingsMode = false; //—Ñ–ª–∞–≥ —Ä–µ–∂–∏–º–∞ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ–Ω—Å–æ—Ä–∞

uint16_t vccVoltage = 0; //–Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è

const uint8_t sleepTime[] = {5, 10, 15, 30, 60};
const char sleepTimeList[] = "–ö–∞–∂–¥—ã–µ 5 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 10 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 15 –º–∏–Ω,–ö–∞–∂–¥—ã–µ 30 –º–∏–Ω,–ö–∞–∂–¥—ã–π 1 —á–∞—Å";

//—Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞
struct sensorData {
  int16_t temp = 0x7FFF; //—Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞
  uint16_t press = 0; //–¥–∞–≤–ª–µ–Ω–∏–µ
  uint8_t hum = 0; //–≤–ª–∞–∂–Ω–æ—Å—Ç—å
} sens;

enum {
  SENS_DHT,
  SENS_DS,
  SENS_BME,
  SENS_SHT,
  SENS_AHT,
  SENS_MAX
};

const char *tempSensList[] = {"DHT", "DS18B20", "BMP/BME", "SHT", "AHT"};

#define REG_READ(reg) (*(volatile uint32*)(reg))

#if (LED_BUILTIN == TWI_SDA_PIN) || (LED_BUILTIN == TWI_SCL_PIN)
#undef STATUS_LED
#define STATUS_LED -1
#endif

#include "WIRE.h"
#include "AHT.h"
#include "SHT.h"
#include "BME.h"
#include "DHT.h"
#include "DS.h"

#include "WIFI.h"
#include "utils.h"

ADC_MODE(ADC_VCC);

void build(void) {
  GP.BUILD_BEGIN(UI_MAIN_THEME, 500);
  GP_FIX_SCRIPTS(); //—Ñ–∏–∫—Å —Å–∫—Ä–∏–ø—Ç–∞ –ø—Ä–æ–≤–µ—Ä–∫–∏ –æ–Ω–ª–∞–π–Ω–∞
  GP_FIX_STYLES(); //—Ñ–∏–∫—Å —Å—Ç–∏–ª–µ–π —Å—Ç—Ä–∞–Ω–∏—Ü—ã

  //–æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –±–ª–æ–∫–æ–≤
  String updateList = "barBat";

  //–Ω–∞—á–∞—Ç—å –º–µ–Ω—é
  GP.UI_MENU("Wireless Sensor", UI_MENU_COLOR);

  //—Å—Å—ã–ª–∫–∏ –º–µ–Ω—é
  GP.UI_LINK("/", "–û–± —É—Å—Ç—Ä–æ–π—Å—Ç–≤–µ");
  if (otaUpdate) GP.UI_LINK("/update", "–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ü–û");
  GP.UI_LINK("/network", "–°–µ—Ç–µ–≤—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏");

  GP_HR(UI_MENU_LINE_COLOR, 6);

  //—Å–æ—Å—Ç–æ—è–Ω–∏–µ —Å–æ–µ–¥–∏–Ω–µ–Ω–∏—è
  if (wifiGetConnectStatus()) {
    updateList += ",bar_wifi";
    GP_BLOCK_SHADOW_BEGIN();
    GP.LABEL("–°–∏–≥–Ω–∞–ª WiFi", "", UI_MENU_TEXT_COLOR, 15);
    GP_LINE_BAR("bar_wifi", getWiFiSignal(), 0, 100, 1, UI_MENU_WIFI_COLOR);
    GP_BLOCK_SHADOW_END();
  }

  GP_FOOTER_BEGIN();
  GP.HR(UI_MENU_LINE_COLOR);
  GP_TEXT_LINK("https://github.com/radon-lab/", "@radon_lab", "user", "#bbb");
  GP.BREAK();
  GP_TEXT_LINK("https://community.alexgyver.ru/threads/chasy-na-gri-v2-alternativnaja-proshivka.5843/", "–û–±—Å—É–∂–¥–µ–Ω–∏–µ –Ω–∞ —Ñ–æ—Ä—É–º–µ", "forum", "#e67b09");
  GP_FOOTER_END();

  GP.UI_BODY(); //–Ω–∞—á–∞—Ç—å –æ—Å–Ω–æ–≤–Ω–æ–µ –æ–∫–Ω–æ

  GP.BOX_BEGIN(GP_JUSTIFY, "auto;padding-left:2%;padding-right:2%");
  GP.LABEL_BLOCK(getBatteryState(), "barBat", UI_BAR_BATTERY_COLOR, 18, 1);

  GP.BOX_BEGIN(GP_RIGHT, "100%");
  if (settings.sensor) {
    if (sens.temp != 0x7FFF) {
      updateList += ",barTemp";
      GP.LABEL_BLOCK(String(sens.temp / 10.0, 1) + "¬∞–°", "barTemp", UI_BAR_TEMP_COLOR, 18, 1);
    }
    if (sens.hum) {
      updateList += ",barHum";
      GP.LABEL_BLOCK(String(sens.hum) + "%", "barHum", UI_BAR_HUM_COLOR, 18, 1);
    }
    if (sens.press) {
      updateList += ",barPress";
      GP.LABEL_BLOCK(String(sens.press) + "mm.Hg", "barPress", UI_BAR_PRESS_COLOR, 18, 1);
    }
  }
  else {
    GP.LABEL_BLOCK("-.-¬∞–°", "barTemp", UI_BAR_TEMP_COLOR, 18, 1);
  }
  GP.BOX_END();

  GP.BOX_END();
  GP.HR(UI_BAR_LINE_COLOR);

  if (ui.uri("/")) { //–∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è –æ —Å–∏—Å—Ç–µ–º–µ
    GP.PAGE_TITLE("–û–± —É—Å—Ç—Ä–æ–π—Å—Ç–≤–µ");

    GP.BLOCK_BEGIN(GP_THIN, "", "–°–∏—Å—Ç–µ–º–Ω–∞—è –∏–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è", UI_BLOCK_COLOR);
    M_BOX(GP.LABEL("–£—Ä–æ–≤–µ–Ω—å —Å–∏–≥–Ω–∞–ª–∞", "", UI_LABEL_COLOR); GP.LABEL("üì∂ " + String(getWiFiSignal()) + '%', "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–†–µ–∂–∏–º –º–æ–¥–µ–º–∞", "", UI_LABEL_COLOR); GP.LABEL(WiFi.getMode() == WIFI_AP ? "AP" : (WiFi.getMode() == WIFI_STA ? "STA" : "AP_STA"), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("MAC –∞–¥—Ä–µ—Å", "", UI_LABEL_COLOR); GP.LABEL(WiFi.macAddress(), "", UI_INFO_COLOR););

    if (wifiGetConnectStatus()) {
      M_BOX(GP.LABEL("–ú–∞—Å–∫–∞ –ø–æ–¥—Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(WiFi.subnetMask().toString(), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("–®–ª—é–∑", "", UI_LABEL_COLOR); GP.LABEL(WiFi.gatewayIP().toString(), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("SSID —Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(StrLengthConstrain(WiFi.SSID(), 12), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("IP —Å–µ—Ç–∏", "", UI_LABEL_COLOR); GP.LABEL(WiFi.localIP().toString(), "", UI_INFO_COLOR););
    }
    if (WiFi.getMode() != WIFI_STA) {
      M_BOX(GP.LABEL("SSID —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞", "", UI_LABEL_COLOR); GP.LABEL(StrLengthConstrain(AP_SSID, 12), "", UI_INFO_COLOR););
      M_BOX(GP.LABEL("IP —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞", "", UI_LABEL_COLOR); GP.LABEL(WiFi.softAPIP().toString(), "", UI_INFO_COLOR););
    }

    GP.BREAK();
    GP_HR_TEXT("–ü–∞–º—è—Ç—å —É—Å—Ç—Ä–æ–π—Å—Ç–≤–∞", "", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("–§—Ä–∞–≥–º–µ–Ω—Ç–∏—Ä–æ–≤–∞–Ω–æ(Heap)", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getHeapFragmentation()) + '%', "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–°–≤–æ–±–æ–¥–Ω–æ(Heap)", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getFreeHeap() / 1000.0, 3) + " kB", "", UI_INFO_COLOR););

    M_BOX(GP.LABEL("–í—Å–µ–≥–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getFlashChipSize() / 1000.0, 1) + " kB", "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ó–∞–Ω—è—Ç–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getSketchSize() / 1000.0, 1) + " kB", "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–°–≤–æ–±–æ–¥–Ω–æ(Flash)", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getFreeSketchSpace() / 1000.0, 1) + " kB", "", UI_INFO_COLOR););

    GP.BREAK();
    GP_HR_TEXT("–û —Å–∏—Å—Ç–µ–º–µ", "", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("ID —á–∏–ø–∞", "", UI_LABEL_COLOR); GP.LABEL("0x" + String(ESP.getChipId(), HEX), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ù–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è", "", UI_LABEL_COLOR); GP.LABEL(String(getBatteryVoltage(), 2) +  + F(" V"), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–ß–∞—Å—Ç–æ—Ç–∞ –ø—Ä–æ—Ü–µ—Å—Å–æ—Ä–∞", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getCpuFreqMHz()) + F(" MHz"), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–¶–∏–∫–ª–æ–≤ –≤ —Å–µ–∫—É–Ω–¥—É", "", UI_LABEL_COLOR); GP.LABEL(String(ESP.getCycleCount()), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("–í—Ä–µ–º—è —Ä–∞–±–æ—Ç—ã", "", UI_LABEL_COLOR); GP.LABEL(getTimeFromMs(millis()), "", UI_INFO_COLOR););

    GP.BREAK();
    GP_HR_TEXT("–í–µ—Ä—Å–∏—è –ü–û", "", UI_LINE_COLOR, UI_HINT_COLOR);

    M_BOX(GP.LABEL("UID", "", UI_LABEL_COLOR); GP.LABEL(getSensorId(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("SDK", "", UI_LABEL_COLOR); GP.LABEL(ESP.getSdkVersion(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("CORE", "", UI_LABEL_COLOR); GP.LABEL(ESP.getCoreVersion(), "", UI_INFO_COLOR););
    M_BOX(GP.LABEL("GyverPortal", "", UI_LABEL_COLOR); GP.LABEL(GP_VERSION, "", UI_INFO_COLOR););

    M_BOX(GP.LABEL("–ü—Ä–æ—à–∏–≤–∫–∞ ESP", "", UI_LABEL_COLOR); GP.LABEL(ESP_FIRMWARE_VERSION, "", UI_INFO_COLOR););
    GP.BLOCK_END();

    String sensorsList = "";

    GP.BLOCK_BEGIN(GP_THIN, "", "–ò–Ω—Ñ–æ—Ä–º–∞—Ü–∏—è –æ –¥–∞—Ç—á–∏–∫–µ", UI_BLOCK_COLOR);
    for (uint8_t i = 0; i < SENS_MAX; i++) {
      if (settings.sensor & (0x01 << i)) {
        if (sensorsList[0] != '\0') sensorsList += '+';
        sensorsList += tempSensList[i];
      }
    }
    M_BOX(GP.LABEL("–î–∞—Ç—á–∏–∫", "", UI_LABEL_COLOR); GP.NUMBER("", (sensorsList[0] == '\0') ? "–û—Ç—Å—É—Ç—Å–≤—É–µ—Ç" : sensorsList, INT32_MAX, "", true););
    M_BOX(GP.LABEL("–ò–Ω—Ç–µ—Ä–≤–∞–ª", "", UI_LABEL_COLOR); GP.SELECT("extPeriod", sleepTimeList, settings.period););
    GP.BLOCK_END();
  }
  else if (ui.uri("/update") && otaUpdate) { //–æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ ESP
    GP.PAGE_TITLE("–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ");

    GP.BLOCK_BEGIN(GP_THIN, "", "–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏", UI_BLOCK_COLOR);
    GP.SPAN("–ü—Ä–æ—à–∏–≤–∫—É –º–æ–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å –≤ Arduino IDE: –°–∫–µ—Ç—á -> –≠–∫—Å–ø–æ—Ä—Ç –±–∏–Ω–∞—Ä–Ω–æ–≥–æ —Ñ–∞–π–ª–∞ (—Å–æ—Ö—Ä–∞–Ω—è–µ—Ç—Å—è –≤ –ø–∞–ø–∫—É —Å –ø—Ä–æ—à–∏–≤–∫–æ–π).", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP.SPAN("–ü–æ–¥–¥–µ—Ä–∂–∏–≤–∞–µ–º—ã–µ —Ñ–æ—Ä–º–∞—Ç—ã —Ñ–∞–π–ª–æ–≤: bin –∏ bin.gz.", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.BREAK();
    GP_HR_TEXT("–ó–∞–≥—Ä—É–∑–∏—Ç—å —Ñ–∞–π–ª—ã", "", UI_LINE_COLOR, UI_HINT_COLOR);
    M_BOX(GP.LABEL("–ü—Ä–æ—à–∏–≤–∫–∞ ESP", "", UI_LABEL_COLOR); GP.OTA_FIRMWARE("", UI_BUTTON_COLOR, true););
    GP.BLOCK_END();
  }
  else { //–ø–æ–¥–∫–ª—é—á–µ–Ω–∏–µ –∫ —Ä–æ—É—Ç–µ—Ä—É
    GP.PAGE_TITLE("–°–µ—Ç–µ–≤—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏");

    GP.BLOCK_BEGIN(GP_THIN, "", "–õ–æ–∫–∞–ª—å–Ω–∞—è —Å–µ—Ç—å WIFI", UI_BLOCK_COLOR);
    if (wifiGetConnectStatus() || wifiGetConnectWaitStatus()) {
      GP.FORM_BEGIN("/network");
      if (wifiGetConnectStatus()) {
        GP.TEXT("", "", settings.ssid, "", 0, "", true);
        GP.BREAK();
        GP.TEXT("", "", WiFi.localIP().toString(), "", 0, "", true);
        GP.SPAN("–ü–æ–¥–∫–ª—é—á–µ–Ω–∏–µ —É—Å—Ç–∞–Ω–æ–≤–ª–µ–Ω–æ", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
      }
      else {
        GP.SPAN(wifiGetConnectState(), GP_CENTER, "syncNetwork", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
        updateList += ",syncNetwork";
      }

      GP.HR(UI_LINE_COLOR);
      if (ui.uri("/connection")) {
        GP.BUTTON_LINK("/", "–í–µ—Ä–Ω—É—Ç—å—Å—è –Ω–∞ –≥–ª–∞–≤–Ω—É—é", UI_BUTTON_COLOR);
      }
      else {
        GP.SUBMIT("–û—Ç–∫–ª—é—á–∏—Ç—å—Å—è", UI_BUTTON_COLOR);
      }
      GP.FORM_END();
    }
    else {
      if (wifiGetScanCompleteStatus()) wifiResetScanCompleteStatus();

      updateList += ",syncReload";
      GP.RELOAD("syncReload");

      GP.FORM_BEGIN("/connection");
      if (ui.uri("/manual")) {
        GP.TEXT("wifiSsid", "SSID", settings.ssid, "", 64);
        GP.BREAK();
        GP.PASS_EYE("wifiPass", "–ü–∞—Ä–æ–ª—å", settings.pass, "100%", 64);
        GP.BREAK();
        GP_TEXT_LINK("/network", "–°–ø–∏—Å–æ–∫ —Å–µ—Ç–µ–π", "net", UI_LINK_COLOR);
        GP.HR(UI_LINE_COLOR);
        GP.SEND("<div style='max-width:300px;justify-content:center' class='inliner'>\n");
        GP.SUBMIT("–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", UI_BUTTON_COLOR);
        GP.BUTTON("extClear", "‚úï", "", (!settings.ssid[0] && !settings.pass[0]) ? GP_GRAY : UI_BUTTON_COLOR, "65px", (boolean)(!settings.ssid[0] && !settings.pass[0]), true);
        GP.SEND("</div>\n");
      }
      else {
        GP.SELECT("wifiNetwork", wifi_scan_list, 0, 0, wifiGetScanFoundStatus());
        GP.BREAK();
        GP.PASS_EYE("wifiPass", "–ü–∞—Ä–æ–ª—å", settings.pass, "100%", 64);
        GP.BREAK();
        GP_TEXT_LINK("/manual", "–†—É—á–Ω–æ–π —Ä–µ–∂–∏–º", "net", UI_LINK_COLOR);
        GP.HR(UI_LINE_COLOR);
        GP.SEND("<div style='max-width:300px;justify-content:center' class='inliner'>\n");
        if (wifiGetScanFoundStatus()) GP.BUTTON("", "–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", "", GP_GRAY, "", true);
        else GP.SUBMIT("–ü–æ–¥–∫–ª—é—á–∏—Ç—å—Å—è", UI_BUTTON_COLOR);
        GP.BUTTON("extScan", "<big><big>‚Üª</big></big>", "", UI_BUTTON_COLOR, "65px", false, true);
        GP.SEND("</div>\n");
      }
      GP.FORM_END();
    }
    GP.BLOCK_END();

    if (ui.uri("/network") && wifiGetConnectStatus()) { //—Å–µ—Ç–µ–≤—ã–µ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
      GP.BLOCK_BEGIN(GP_THIN, "", "–û—Ç–ø—Ä–∞–≤–∫–∞ –¥–∞–Ω–Ω—ã—Ö", UI_BLOCK_COLOR);
      for (uint8_t i = 0; i < (MAX_CLOCK * 2); i += 2) {
        if (i) {
          GP.HR(UI_MENU_LINE_COLOR);
        }
        if (settings.send[i][0] != '\0') {
          M_BOX(
            M_BOX(GP_LEFT, GP.TEXT("", "IP –∞–¥—Ä–µ—Å", settings.send[i], "", 20, "", true); GP.TEXT("", "–ù–∞–∑–≤–∞–Ω–∏–µ", (settings.send[i + 1][0] != '\0') ? settings.send[i + 1] : settings.send[i], "", 20, "", true););
            GP.BUTTON_MINI(String("extSendDel/") + i, "–£–¥–∞–ª–∏—Ç—å", "", UI_BUTTON_COLOR, "115px!important", false, true);
          );
        }
        else {
          M_BOX(
            M_BOX(GP_LEFT, GP.TEXT("extSendIp", "IP –∞–¥—Ä–µ—Å", "", "", 15); GP.TEXT("extSendName", "–ù–∞–∑–≤–∞–Ω–∏–µ", "", "", 19););
            GP.BUTTON_MINI("extSendAdd", "–î–æ–±–∞–≤–∏—Ç—å", "", UI_BUTTON_COLOR, "115px!important", false, true);
          );
          buffSendIp[0] = '\0';
          buffSendName[0] = '\0';
          break;
        }
      }
      GP.BLOCK_END();
    }
  }

  GP.UPDATE(updateList);
  GP.UI_END(); //–∑–∞–≤–µ—Ä—à–∏—Ç—å –æ–∫–Ω–æ –ø–∞–Ω–µ–ª–∏ —É–ø—Ä–∞–≤–ª–µ–Ω–∏—è

  GP_BUILD_END();
}

void buildUpdater(bool UpdateEnd, const String & UpdateError) {
  GP.BUILD_BEGIN(UI_MAIN_THEME, 500);
  GP_FIX_SCRIPTS(); //—Ñ–∏–∫—Å —Å–∫—Ä–∏–ø—Ç–∞ –ø—Ä–æ–≤–µ—Ä–∫–∏ –æ–Ω–ª–∞–π–Ω–∞
  GP_FIX_STYLES(); //—Ñ–∏–∫—Å —Å—Ç–∏–ª–µ–π —Å—Ç—Ä–∞–Ω–∏—Ü—ã

  GP.PAGE_TITLE("–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ");

  GP.BLOCK_BEGIN(GP_THIN, "", "–û–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏", UI_BLOCK_COLOR);
  if (!UpdateEnd) {
    GP.SPAN("<b>–ü—Ä–æ—à–∏–≤–∫—É –º–æ–∂–Ω–æ –ø–æ–ª—É—á–∏—Ç—å –≤ Arduino IDE: –°–∫–µ—Ç—á -> –≠–∫—Å–ø–æ—Ä—Ç –±–∏–Ω–∞—Ä–Ω–æ–≥–æ —Ñ–∞–π–ª–∞ (—Å–æ—Ö—Ä–∞–Ω—è–µ—Ç—Å—è –≤ –ø–∞–ø–∫—É —Å –ø—Ä–æ—à–∏–≤–∫–æ–π).</b><br>–ü–æ–¥–¥–µ—Ä–∂–∏–≤–∞–µ–º—ã–µ —Ñ–æ—Ä–º–∞—Ç—ã —Ñ–∞–π–ª–æ–≤ bin –∏ bin.gz.", GP_CENTER, "", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.HR(UI_LINE_COLOR);
    M_BOX(GP_CENTER, GP.OTA_FIRMWARE("", UI_BUTTON_COLOR, true); GP.BUTTON_MINI_LINK("/", "–í–µ—Ä–Ω—É—Ç—å—Å—è –Ω–∞ –≥–ª–∞–≤–Ω—É—é", UI_BUTTON_COLOR););
  }
  else if (UpdateError.length()) {
    GP.SPAN("<big><b>–ü—Ä–æ–∏–∑–æ—à–ª–∞ –æ—à–∏–±–∫–∞ –ø—Ä–∏ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–∏...</b></big><br><small>[" + UpdateError + "]</small>", GP_CENTER, "", GP_RED); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.HR(UI_LINE_COLOR);
    M_BOX(GP_CENTER, GP_BUTTON_MINI_LINK("/ota_update", "‚†Ä<big><big>‚Üª</big></big>‚†Ä", UI_BUTTON_COLOR); GP.BUTTON_MINI_LINK("/", "–í–µ—Ä–Ω—É—Ç—å—Å—è –Ω–∞ –≥–ª–∞–≤–Ω—É—é", UI_BUTTON_COLOR););
  }
  else {
    GP.SPAN("<big><b>–í—ã–ø–æ–ª–Ω—è–µ—Ç—Å—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –ø—Ä–æ—à–∏–≤–∫–∏...</b></big>", GP_CENTER, "syncUpdate", UI_INFO_COLOR); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.SPAN("<small>–ù–µ –≤—ã–∫–ª—é—á–∞–π—Ç–µ —É—Å—Ç—Ä–æ–π—Å—Ç–≤–æ –¥–æ –∑–∞–≤–µ—Ä—à–µ–Ω–∏—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è!</small>", GP_CENTER, "syncWarn", GP_RED); //–æ–ø–∏—Å–∞–Ω–∏–µ
    GP.HR(UI_LINE_COLOR);
    M_BOX(GP_CENTER, GP.BUTTON_MINI_LINK("/", "–í–µ—Ä–Ω—É—Ç—å—Å—è –Ω–∞ –≥–ª–∞–≤–Ω—É—é", UI_BUTTON_COLOR););
    GP.UPDATE("syncUpdate,syncWarn");
  }
  GP.BLOCK_END();

  GP_BUILD_END();
}

void action() {
  if (ui.click()) {
    if (ui.clickSub("ext")) {
      if (ui.clickSub("extSendDel")) {
        for (uint8_t multiNum = constrain(ui.clickNameSub(1).toInt(), 0, ((MAX_CLOCK - 1) * 2)); multiNum < ((MAX_CLOCK - 1) * 2); multiNum++) {
          strncpy(settings.send[multiNum], settings.send[multiNum + 2], 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
        }
        settings.send[((MAX_CLOCK - 1) * 2)][0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        settings.send[((MAX_CLOCK - 1) * 2) + 1][0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª

        if (settings.send[2][0] == '\0') {
          settings.send[0][0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
          settings.send[1][0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        }
        memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
      if (ui.click("extSendAdd")) {
        if (buffSendIp[0] != '\0') { //–µ—Å–ª–∏ —Å—Ç—Ä–æ–∫–∞ –Ω–µ –ø—É—Å—Ç–∞—è
          if (!WiFi.localIP().toString().equals(buffSendIp)) { //–µ—Å–ª–∏ –Ω–µ —Å–æ–±—Å—Ç–≤–µ–Ω–Ω—ã–π –∞–¥—Ä–µ—Å
            for (uint8_t i = 0; i < (MAX_CLOCK * 2); i += 2) {
              if (settings.send[i][0] == '\0') { //–µ—Å–ª–∏ —è—á–µ–π–∫–∞ –Ω–µ –∑–∞–ø–æ–ª–Ω–µ–Ω–∞
                sendHostNum = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ç–µ–∫—É—â–∏–π —Ö–æ—Å—Ç
                strncpy(settings.send[i], buffSendIp, 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
                settings.send[i][19] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
                strncpy(settings.send[i + 1], buffSendName, 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
                settings.send[i + 1][19] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
                memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
                break;
              }
              else if (String(settings.send[i]).equals(buffSendIp)) break;
            }
          }
        }
      }

      if (ui.click("extSendIp")) {
        strncpy(buffSendIp, ui.getString("extSendIp").c_str(), 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
        buffSendIp[19] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      }
      if (ui.click("extSendName")) {
        strncpy(buffSendName, ui.getString("extSendName").c_str(), 20); //–∫–æ–ø–∏—Ä—É–µ–º —Å–µ–±–µ
        buffSendName[19] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      }

      if (ui.click("extClear")) {
        settings.ssid[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        settings.pass[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
      if (ui.click("extScan")) {
        if (wifiGetScanAllowStatus()) wifiStartScanNetworks(); //–Ω–∞—á–∏–Ω–∞–µ–º –ø–æ–∏—Å–∫
      }

      if (ui.click("extPeriod")) {
        settings.period = ui.getInt("extPeriod");
        memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
    }
  }
  /**************************************************************************/
  if (ui.form()) {
    if (!wifiGetConnectWaitStatus() && !wifiGetConnectStatus()) {
      if (ui.form("/connection")) {
        wifiSetConnectWaitInterval(1); //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –∏–Ω—Ç–µ—Ä–≤–∞–ª –ø–µ—Ä–µ–ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è
        if (!ui.copyStr("wifiSsid", settings.ssid, 64)) { //–∫–æ–ø–∏—Ä—É–µ–º –∏–∑ —Å—Ç—Ä–æ–∫–∏
          int network = 0; //–Ω–æ–º–µ—Ä —Å–µ—Ç–∏ –∏–∑ —Å–ø–∏—Å–∫–∞
          if (ui.copyInt("wifiNetwork", network)) strncpy(settings.ssid, WiFi.SSID(network).c_str(), 64); //–∫–æ–ø–∏—Ä—É–µ–º –∏–∑ —Å–ø–∏—Å–∫–∞
          else wifiSetConnectWaitInterval(0); //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –∏–Ω—Ç–µ—Ä–≤–∞–ª –ø–µ—Ä–µ–ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è
        }
        settings.ssid[63] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        ui.copyStr("wifiPass", settings.pass, 64); //–∫–æ–ø–∏—Ä—É–µ–º –ø–∞—Ä–æ–ª—å —Å–µ—Ç–∏
        settings.pass[63] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
        memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
      }
    }
    else if (ui.form("/network")) {
      wifiResetConnectStatus(); //–æ—Ç–∫–ª—é—á–∞–µ–º—Å—è –æ—Ç —Ç–æ—á–∫–∏ –¥–æ—Å—Ç—É–ø–∞
      settings.ssid[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      settings.pass[0] = '\0'; //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–æ—Å–ª–µ–¥–Ω–∏–π —Å–∏–º–≤–æ–ª
      memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
    }
  }
  /**************************************************************************/
  if (ui.update()) {
    if (ui.updateSub("sync")) {
      if (ui.update("syncNetwork")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(wifiGetConnectState());
      }
      if (ui.update("syncReload") && wifiGetScanCompleteStatus()) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(1);
        wifiResetScanCompleteStatus();
      }
    }
    //--------------------------------------------------------------------
    if (ui.updateSub("bar")) {
      if (ui.update("barBat")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(getBatteryState());
      }

      if (ui.update("barTemp")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.temp / 10.0, 1) + "¬∞–°");
      }
      if (ui.update("barHum")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.hum) + "%");
      }
      if (ui.update("barPress")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(String(sens.press) + "mm.Hg");
      }

      if (ui.update("bar_wifi")) { //–µ—Å–ª–∏ –±—ã–ª–æ –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
        ui.answer(getWiFiSignal());
      }
    }
  }
}
//---------------------------–ü–æ–ª—É—á–∏—Ç—å –Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏---------------------------------
float getBatteryVoltage(void) { //–ø–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  return (vccVoltage / 1000.0);
}
//---------------------------–ü–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏----------------------------------
uint8_t getBatteryCharge(void) { //–ø–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  if (vccVoltage < BAT_VOLTAGE_MIN) return 0;
  return map(constrain(vccVoltage, BAT_VOLTAGE_MIN, BAT_VOLTAGE_MAX), BAT_VOLTAGE_MAX, BAT_VOLTAGE_MIN, 20, 0) * 5;
}
//---------------------------–ü–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏----------------------------------
String getBatteryState(void) { //–ø–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  String data = "üîã ";
  data += getBatteryCharge();
  data += '%';
  return data;
}
//---------------------------–ü–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏----------------------------------
uint8_t getWiFiSignal(void) { //–ø–æ–ª—É—á–∏—Ç—å —Å–æ—Å—Ç–æ—è–Ω–∏–µ –±–∞—Ç–∞—Ä–µ–∏
  return constrain(2 * (WiFi.RSSI() + 100), 0, 100);
}
//--------------------------------------------------------------------
String getTimeFromMs(uint32_t data) {
  data /= 1000;

  uint8_t second = data % 60;
  data /= 60;
  uint8_t minute = data % 60;
  data /= 60;
  uint16_t hour = data % 24;
  data /= 24;

  String str = "";

  str += data;
  str += ':';
  str += hour;
  str += ':';
  str += minute / 10;
  str += minute % 10;
  str += ':';
  str += second / 10;
  str += second % 10;

  return str;
}
//--------------------------------------------------------------------
String StrLengthConstrain(String data, uint8_t size) {
  if (data.length() > size) {
    data.remove(size);
    data += "‚Ä¶";
  }
  return data;
}
//--------------------------------------------------------------------
char getHexChar(uint8_t hex) {
  if (hex > 15) return 'F';
  if (hex > 9) return ('A' + (hex - 10));
  return ('0' + hex);
}
//--------------------------------------------------------------------
String getSensorId(void) {
  String str;
  str.reserve(20);
  str = "";

  uint8_t mac[6];
  efuseGetDefaultMacAddress(mac);

  for (uint8_t i = 0; i < 6; i++) {
    str += getHexChar(mac[i] >> 4);
    str += getHexChar(mac[i] & 0x0F);
    if (i < 5) str += ':';
  }

  return str;
}
//--------------------------------------------------------------------
void efuseGetDefaultMacAddress(uint8_t* mac) {
  uint32_t _efuse_low = REG_READ(0x3FF00050);
  uint32_t _efuse_mid = REG_READ(0x3FF00054);
  uint32_t _efuse_high = REG_READ(0x3FF0005C);

  mac[0] = _efuse_high >> 16;
  mac[1] = _efuse_high >> 8;
  mac[2] = _efuse_high;
  mac[3] = _efuse_mid >> 8;
  mac[4] = _efuse_mid;
  mac[5] = _efuse_low >> 24;
}
//--------------------------------------------------------------------
void checkCRC(uint8_t* crc, uint8_t data) { //—Å–≤–µ—Ä–∫–∞ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω–æ–π —Å—É–º–º—ã
  for (uint8_t i = 0; i < 8; i++) { //—Å—á–∏—Ç–∞–µ–º –¥–ª—è –≤—Å–µ—Ö –±–∏—Ç
    *crc = ((*crc ^ data) & 0x01) ? (*crc >> 0x01) ^ 0x8C : (*crc >> 0x01); //—Ä–∞—Å—Å—á–∏—Ç—ã–≤–∞–µ–º –∑–Ω–∞—á–µ–Ω–∏–µ
    data >>= 0x01; //—Å–¥–≤–∏–≥–∞–µ–º –±—É—Ñ–µ—Ä
  }
}
//--------------------------------------------------------------------
void checkSettingsButton(void) {
  if (ESP.rtcUserMemoryRead(32, rtcMemory, sizeof(rtcMemory))) {
    if ((rtcMemory[0] ^ rtcMemory[1]) != 0xFFFFFFFF) {
      rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = 0xFFFFFFFF; //—Å–±—Ä–æ—Å–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
#if DEBUG_MODE
      Serial.println F("Settings button reset!");
#endif
    }
    else {
      rtcMemory[0]++; //–¥–æ–±–∞–≤–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏–µ –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = rtcMemory[0] ^ 0xFFFFFFFF; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
#if DEBUG_MODE
      Serial.print F("Settings button click ");
      Serial.print(rtcMemory[0]);
      Serial.println F("...");
#endif
    }
    if (rtcMemory[0] >= 3) { //–µ—Å–ª–∏ –±—ã–ª–æ –¥–æ—Å—Ç–∞—Ç–æ—á–Ω–æ–µ –∫–æ–ª–∏—á–µ—Å—Ç–≤–æ –Ω–∞–∂–∞—Ç–∏–π
      rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
      rtcMemory[1] = 0xFFFFFFFF; //—Å–±—Ä–æ—Å–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
      settingsMode = true; //–≤–∫–ª—é—á–∏—Ç—å —Ä–µ–∂–∏–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
#if DEBUG_MODE
      Serial.println F("Settings mode enable...");
#endif
    }
  }
  if (ESP.rtcUserMemoryWrite(32, rtcMemory, sizeof(rtcMemory))) {
#if DEBUG_MODE
    Serial.println F("Settings button update!");
#endif
  }
}
//--------------------------------------------------------------------
void resetSettingsButton(void) {
  if (rtcMemory[0]) { //–µ—Å–ª–∏ –∫–Ω–æ–ø–∫–∞ –±—ã–ª–∞ –Ω–∞–∂–∞—Ç–∞
    rtcMemory[0] = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏
    rtcMemory[1] = 0xFFFFFFFF; //—Å–±—Ä–æ—Å–∏–ª–∏ –∫–æ–Ω—Ç—Ä–æ–ª—å–Ω—É—é —Å—É–º–º—É
    if (ESP.rtcUserMemoryWrite(32, rtcMemory, sizeof(rtcMemory))) {
#if DEBUG_MODE
      Serial.println F("Settings button reset!");
#endif
    }
  }
}
//--------------------------------------------------------------------
void powerDown(void) {
#if DEBUG_MODE
  Serial.println F("Power down...");
#endif

  delay(100); //–∂–¥–µ–º –æ–∫–æ–Ω—á–∞–Ω–∏—è –ø–µ—Ä–µ–¥–∞—á–∏

  ui.stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—å ui
  udp.stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏—Ç—å udp

  WiFi.disconnect(); //–æ—Ç–∫–ª—é—á–∞–µ–º—Å—è –æ—Ç —Å–µ—Ç–∏
  WiFi.mode(WIFI_OFF); //–æ—Ç–∫–ª—é—á–∞–µ–º wifi

  ESP.deepSleep(60e6 * sleepTime[settings.period]); //—É—Ö–æ–¥–∏–º –≤ —Å–æ–Ω
}
//--------------------------------------------------------------------
void lowBattery(void) {
#if STATUS_LED > 0
  for (uint8_t i = 0; i < 5; i++) {
    digitalWrite(LED_BUILTIN, (boolean)(i & 0x01));
    delay(200);
  }
  digitalWrite(LED_BUILTIN, HIGH);
#endif
  ESP.deepSleep(0); //—É—Ö–æ–¥–∏–º –≤ —Å–æ–Ω
}
//--------------------------------------------------------------------
void checkSensors(void) {
#if DEBUG_MODE
  Serial.println F("Sensor check start...");
#endif

  if (settingsMode == true) settings.sensor = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –Ω–∞–π–¥–µ–Ω–Ω—ã–µ —Å–µ–Ω—Å–æ—Ä—ã

  sens.temp = 0x7FFF; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—É
  sens.press = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –¥–∞–≤–ª–µ–Ω–∏–µ
  sens.hum = 0; //—Å–±—Ä–æ—Å–∏–ª–∏ –≤–ª–∞–∂–Ω–æ—Å—Ç—å

  sensorReady = false; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ñ–ª–∞–≥ –≥–æ—Ç–æ–≤–Ω–æ—Å—Ç–∏ –∑–∞–º–µ—Ä–∞

  if (!settings.sensor) {
    if (!readTempDHT()) {
      if (!readTempDS()) {
        readTempBME();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
        readTempSHT();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
        readTempAHT();
        twi_write_stop(); //–æ—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —à–∏–Ω—É
      }
    }
    if (settings.sensor) {
      memory.updateNow(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
#if DEBUG_MODE
      Serial.println F("Sensor found, update memory...");
#endif
    }
    else {
#if DEBUG_MODE
      Serial.print F("Sensor not found");
#endif
      if (settingsMode == true) {
        memory.update(); //–æ–±–Ω–æ–≤–∏—Ç—å –¥–∞–Ω–Ω—ã–µ –≤ –ø–∞–º—è—Ç–∏
#if DEBUG_MODE
        Serial.println F(", update memory...");
#endif
      }
#if DEBUG_MODE
      else Serial.println F("!");
#endif
    }
  }
  else {
    for (uint8_t sensor = 0; sensor < SENS_MAX; sensor++) {
      if (settings.sensor & (0x01 << sensor)) {
        switch (sensor) {
          case SENS_DHT: readTempDHT(); return;
          case SENS_DS: readTempDS(); return;
          case SENS_BME: readTempBME(); twi_write_stop(); break;
          case SENS_SHT: readTempSHT(); twi_write_stop(); break;
          case SENS_AHT: readTempAHT(); twi_write_stop(); break;
        }
      }
    }
  }
}
//--------------------------------------------------------------------
void updateSensors(void) {
  updateTempDS(); //–∑–∞–±—Ä–∞—Ç—å —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä—É DS18B20

  if (sens.temp != 0x7FFF) { //–µ—Å–ª–∏ —Ç–µ–º–ø–µ—Ä–∞—Ç—É—Ä–∞ –¥–æ—Å—Ç—É–ø–Ω–∞
    if (sens.temp > 850) sens.temp = 850; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª
    if (sens.temp < -850) sens.temp = -850; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª
  }
  if (sens.hum > 99) sens.hum = 99; //–µ—Å–ª–∏ –≤—ã—à–ª–∏ –∑–∞ –ø—Ä–µ–¥–µ–ª

  efuseGetDefaultMacAddress(buffSendData); //–ø–æ–ª—É—á–∏—Ç—å mac –∞–¥—Ä–µ—Å

  buffSendData[6] = (settingsMode == true) ? UDP_FOUND_CMD : UDP_WRITE_CMD;

  buffSendData[7] = (uint8_t)sens.temp;
  buffSendData[8] = (uint8_t)(sens.temp >> 8);

  buffSendData[9] = (uint8_t)sens.press;
  buffSendData[10] = (uint8_t)(sens.press >> 8);

  buffSendData[11] = (uint8_t)sens.hum;

  buffSendData[12] = (uint8_t)getBatteryCharge();
  buffSendData[13] = (uint8_t)getWiFiSignal();
  buffSendData[14] = (uint8_t)sleepTime[settings.period];

  uint8_t crc = 0;
  for (uint8_t i = 0; i < (UDP_SEND_SIZE - 1); i++) checkCRC(&crc, buffSendData[i]);

  buffSendData[15] = (uint8_t)crc;

  sensorReady = true; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —Ñ–ª–∞–≥ –≥–æ—Ç–æ–≤–Ω–æ—Å—Ç–∏ –∑–∞–º–µ—Ä–∞

#if DEBUG_MODE
  Serial.println F("Sensor check end...");
#endif
}
//--------------------------------------------------------------------
void sendUpdate(void) {
  if (wifiGetConnectStatus() && (sendReady == true) && (sensorReady == true)) {
    if (sendHostNum < (MAX_CLOCK * 2)) {
      if ((settings.send[sendHostNum][0] != '\0') || !sendHostNum) {
#if DEBUG_MODE
        Serial.print F("Send data to [ ");
        Serial.print((settings.send[sendHostNum][0] != '\0') ? settings.send[sendHostNum] : UDP_BROADCAST_ADDR);
        Serial.println F(" ]...");
#endif
        if (!udp.beginPacket((settings.send[sendHostNum][0] != '\0') ? settings.send[sendHostNum] : UDP_BROADCAST_ADDR, UDP_CLOCK_PORT) || (udp.write(buffSendData, UDP_SEND_SIZE) != UDP_SEND_SIZE) || !udp.endPacket()) {
          sendReady = false; //—Å–±—Ä–æ—Å–∏–ª–∏ —Ñ–ª–∞–≥ –ø–æ–≤—Ç–æ—Ä–Ω–æ–π –ø–æ–ø—ã—Ç–∫–∏ –æ—Ç–ø—Ä–∞–≤–∫–∏ –¥–∞–Ω–Ω—ã—Ö
#if DEBUG_MODE
          Serial.println F("Send data fail!");
#endif
        }
        else {
          sendHostNum += 2;
#if DEBUG_MODE
          Serial.println F("Send data ok...");
#endif
        }
      }
      else {
        sendHostNum = (MAX_CLOCK * 2);
#if DEBUG_MODE
        Serial.println F("Send all data completed...");
#endif
      }
    }
    else if (settingsMode == false) powerDown(); //–æ—Ç–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ
  }
}
//--------------------------------------------------------------------
void timeUpdate(void) {
  static uint8_t updateTimer = 0;
  static uint32_t secondsTimer = millis();

  if ((millis() - secondsTimer) >= 1000) {
    secondsTimer = millis();

    if (updateTimer < 255) updateTimer++; //–ø—Ä–∏–±–∞–≤–∏–ª–∏ —Ç–∞–π–º–µ—Ä —Å–µ–∫—É–Ω–¥

    if (updateTimer == 2) checkSensors(); //–ø—Ä–æ–≤–µ—Ä–∫–∞ –¥–æ—Å—Ç—É–ø–Ω–æ—Å—Ç–∏ —Å–µ–Ω—Å–æ—Ä–æ–≤
    else if (updateTimer == 3) updateSensors(); //–æ–±–Ω–æ–≤–∏—Ç—å –ø–æ–∫–∞–∑–∞–Ω–∏—è —Å–µ–Ω—Å–æ—Ä–æ–≤
    else if ((updateTimer > 15) && (settingsMode == true) && ui.online()) updateTimer = 15; //—Å–±—Ä–æ—Å–∏—Ç—å —Ç–∞–π–º–µ—Ä
    else if (updateTimer > 30) powerDown(); //–æ—Ç–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ

    sendReady = true; //—É—Å—Ç–∞–Ω–æ–≤–∏–ª–∏ —Ñ–ª–∞–≥ –ø–æ–≤—Ç–æ—Ä–Ω–æ–π –ø–æ–ø—ã—Ç–∫–∏ –æ—Ç–ø—Ä–∞–≤–∫–∏ –¥–∞–Ω–Ω—ã—Ö

    resetSettingsButton(); //—Å–±—Ä–æ—Å–∏—Ç—å –Ω–∞–∂–∞—Ç–∏—è –∫–Ω–æ–ø–∫–∏ –Ω–∞—Å—Ç—Ä–æ–µ–∫

#if STATUS_LED > 0
    if (settingsMode == true) {
      if (!wifiGetConnectStatus() && wifiGetConnectWaitStatus()) digitalWrite(LED_BUILTIN, !digitalRead(LED_BUILTIN)); //–º–∏–≥–∞–µ–º –∏–Ω–¥–∏–∫–∞—Ü–∏–µ–π
    }
#endif
  }
}
//--------------------------------------------------------------------
void setup() {
  //–≤—ã–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ wifi
  WiFi.forceSleepBegin();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –∏–Ω–¥–∏–∫–∞—Ç–æ—Ä–∞
#if STATUS_LED > 0
  pinMode(LED_BUILTIN, OUTPUT);
  digitalWrite(LED_BUILTIN, HIGH);
#endif

  //–æ–±–Ω–æ–≤–∏—Ç—å –Ω–∞–ø—Ä—è–∂–µ–Ω–∏–µ –ø–∏—Ç–∞–Ω–∏—è
  vccVoltage = ESP.getVcc();
  if (vccVoltage > BAT_VOLTAGE_CORRECT) vccVoltage -= BAT_VOLTAGE_CORRECT;
  if (!getBatteryCharge()) lowBattery();

#if DEBUG_MODE
  Serial.begin(115200);
  Serial.println F("");
  Serial.println F("Startup...");
  Serial.print F("Supply voltage ");
  Serial.print(getBatteryVoltage(), 2);
  Serial.println F("...");
  Serial.print F("Firmware version ");
  Serial.print F(ESP_FIRMWARE_VERSION);
  Serial.println F("...");
#endif

  //–ø—Ä–æ–≤–µ—Ä–∫–∞ –∫–Ω–æ–ø–∫–∏ —Ä–µ–∂–∏–º–∞ –Ω–∞—Å—Ç—Ä–æ–π–∫–∏
  checkSettingsButton();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –∏–Ω–¥–∏–∫–∞—Ç–æ—Ä–∞
#if STATUS_LED > 0
  if (settingsMode == true) digitalWrite(LED_BUILTIN, LOW);
#endif

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è —à–∏–Ω—ã
  pinMode(TWI_SDA_PIN, INPUT_PULLUP);
  pinMode(TWI_SCL_PIN, INPUT_PULLUP);
  twi_init();

  //–∏–Ω–∏—Ü–∏–∞–ª–∏–∑–∞—Ü–∏—è –æ–±–Ω–æ–≤–ª–µ–Ω–∏—è –ø–æ OTA
  if (settingsMode == true) {
    if ((getBatteryCharge() >= 50) && (ESP.getFreeSketchSpace() < ESP.getSketchSize())) {
      otaUpdate = false; //–≤—ã–∫–ª—é—á–∞–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ
#if DEBUG_MODE
      Serial.println F("OTA update disable, running out of memory");
#endif
    }
#if DEBUG_MODE
    else Serial.println F("OTA update enable");
#endif
  }

  //—Å–±—Ä–∞—Å—ã–≤–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ –≥—Ä—É–ø–ø–æ–≤–æ–≥–æ —É–ø—Ä–∞–≤–ª–µ–Ω–∏—è
  for (uint8_t i = 0; i < (MAX_CLOCK * 2); i++) settings.send[i][0] = '\0';

  //—É—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –ø–µ—Ä–∏–æ–¥ –ø–æ —É–º–æ–ª—á–∞–Ω–∏—é
  settings.period = SEND_DATA_PERIOD;

  //–≤–æ—Å—Å—Ç–∞–Ω–∞–≤–ª–∏–≤–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ —Å–µ—Ç–∏
  strncpy(settings.ssid, WiFi.SSID().c_str(), 64);
  settings.ssid[63] = '\0';
  strncpy(settings.pass, WiFi.psk().c_str(), 64);
  settings.pass[63] = '\0';

  //—á–∏—Ç–∞–µ–º –Ω–∞—Å—Ç—Ä–æ–π–∫–∏ –∏–∑ –ø–∞–º—è—Ç–∏
  EEPROM.begin(memory.blockSize());
  memory.begin(0, 0xAB);

  //–ø–æ–¥–∫–ª—é—á–∞–µ–º –∫–æ–Ω—Å—Ç—Ä—É–∫—Ç–æ—Ä –∏ –∑–∞–ø—É—Å–∫–∞–µ–º –≤–µ–± –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å
  if (settingsMode == true) {
    ui.attachBuild(build);
    ui.attach(action);
    ui.start();

    //–Ω–∞—Å—Ç—Ä–∞–∏–≤–∞–µ–º –æ–±–Ω–æ–≤–ª–µ–Ω–∏–µ –±–µ–∑ –ø–∞—Ä–æ–ª—è
    if (otaUpdate) {
      ui.enableOTA();
      ui.OTA.attachUpdateBuild(buildUpdater);
    }
    ui.downloadAuto(true);
    ui.uploadAuto(false);
  }

  //–≤–∫–ª—é—á–∏—Ç—å –ø–∏—Ç–∞–Ω–∏–µ wifi
  WiFi.forceSleepWake();

  //–Ω–∞—Å—Ç—Ä–∞–∏–≤–∞–µ–º wifi
  if (WiFi.getAutoConnect() != false) WiFi.setAutoConnect(false);
  if (WiFi.getAutoReconnect() != true) WiFi.setAutoReconnect(true);
  if (settingsMode == true) wifiStartAP();
  else WiFi.mode(WIFI_STA);
}
//--------------------------------------------------------------------
void loop() {
  wifiUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ —Å—Ç–∞—Ç—É—Å–æ–≤ wifi
  timeUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –≤—Ä–µ–º–µ–Ω–∏
  sendUpdate(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –∫–∞–Ω–∞–ª–∞ —Å–≤—è–∑–∏

  if (settingsMode == true) {
    ui.tick(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –≤–µ–± –∏–Ω—Ç–µ—Ä—Ñ–µ–π—Å–∞
    memory.tick(); //–æ–±—Ä–∞–±–æ—Ç–∫–∞ –µ–µ–ø—Ä–æ–º
  }
}
